/*
 * Black Forest for The Cartographers.
 * Edited by T-West, Novemeber 2023.
 */

start_random
  percent_chance 35 #define GRASS_MAP
  percent_chance 20 #define AUTUMN_MAP
  percent_chance 15 #define SNOW_AUTUMN_MAP
  percent_chance 20 #define FROZEN_MAP
  percent_chance 10 #define JUNGLE_MAP
end_random

if GRASS_MAP
  #const BASE_TERRAIN 0 /* GRASS */
  #const BASE_FOREST 10 /* FOREST */
  #const MIXING_FOREST 19 /* PINE_FOREST */
  #const GRASS_PATCH 9 /* GRASS3 */
  #const ROAD_TERRAIN 25 /* ROAD2 */
  #const SHALLOW_TERRAIN 4 /* SHALLOW */
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
  #const HUNTABLE 65 /* DEER */
  #const LUREABLE 48 /* BOAR */
  #const PREDATOR 126 /* WOLF */
  #const STRAGGLER 349 /* OAKTREE */
  #const BIRD 96 /* HAWK */
elseif AUTUMN_MAP
  #const BASE_TERRAIN 0 /* GRASS */
  #const BASE_FOREST 104 /* DLC_FORESTAUTUMN */
  #const MIXING_FOREST 110 /* BIRCH_FOREST */
  #const GRASS_PATCH 9 /* GRASS3 */
  #const ROAD_TERRAIN 25 /* ROAD2 */
  #const SHALLOW_TERRAIN 4 /* SHALLOW */
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1243 /* DLC_GOOSE */
  #const LUREABLE 48 /* BOAR */
  #const HUNTABLE 1239 /* DLC_IBEX */
  #const PREDATOR 126 /* WOLF */
  #const STRAGGLER 1248 /* DLC_AUTUMNTREE */
  #const BIRD 96 /* HAWK */
elseif SNOW_AUTUMN_MAP
  #define MASK_LEAVES
  #const BASE_TERRAIN 32 /* SNOW */
  #const BASE_FOREST 105 /* DLC_FORESTSNOWAUTUMN */
  #const MIXING_FOREST 104 /* DLC_FORESTAUTUMN */
  #const GRASS_PATCH 34 /* GRASS_SNOW */
  #const ROAD_TERRAIN 38 /* Road, Snow */
  #const SHALLOW_TERRAIN 35 /* ICE */
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1243 /* DLC_GOOSE */
  #const LUREABLE 48 /* BOAR */
  #const HUNTABLE 1239 /* DLC_IBEX */
  #const PREDATOR 126 /* WOLF */
  #const STRAGGLER 1249 /* DLC_AUTUMNSNOWTREE */
  #const BIRD 96 /* HAWK */
elseif FROZEN_MAP
  #define MASK_LEAVES
  #const BASE_TERRAIN 32 /* SNOW */
  #const BASE_FOREST 21 /* SNOW_FOREST */
  #const MIXING_FOREST 19 /* PINE_FOREST */
  #const GRASS_PATCH 34 /* GRASS_SNOW */
  #const ROAD_TERRAIN 38 /* Road, Snow */
  #const SHALLOW_TERRAIN 35 /* ICE */
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
  #const LUREABLE 48 /* BOAR */
  #const HUNTABLE 65 /* DEER */
  #const PREDATOR 126 /* WOLF */
  #const STRAGGLER 413 /* SNOWPINETREE */
  #const BIRD 96 /* HAWK */
elseif JUNGLE_MAP
  #const BASE_TERRAIN 0 /* GRASS */
  #const BASE_FOREST 17 /* JUNGLE */
  #const MIXING_FOREST 17 /* JUNGLE */
  #const GRASS_PATCH 9 /* GRASS3 */
  #const ROAD_TERRAIN 39 /* Road, Fungus */
  #const SHALLOW_TERRAIN 4 /* SHALLOW */
  #const HERDABLE_A 833 /* TURKEY */
  #const HERDABLE_B 305 /* DLC_LLAMA */
  #const LUREABLE 48 /* JAVELINA */
  #const HUNTABLE 65 /* DEER */
  #const PREDATOR 812 /* JAGUAR */
  #const STRAGGLER 414 /* JUNGLETREE */
  #const BIRD 816 /* MACAW */
endif

/*
 * A medium map produces 5 groups of Wolfs.
 * These random blocks are used to randomize the number of Wolfs for each
 * individual group.
 */
start_random
  percent_chance 75 #define PREDATOR_A_1
  percent_chance 25 #define PREDATOR_A_2
end_random
start_random
  percent_chance 75 #define PREDATOR_B_1
  percent_chance 25 #define PREDATOR_B_2
end_random
start_random
  percent_chance 75 #define PREDATOR_C_1
  percent_chance 25 #define PREDATOR_C_2
end_random
start_random
  percent_chance 75 #define PREDATOR_D_1
  percent_chance 25 #define PREDATOR_D_2
end_random
start_random
  percent_chance 75 #define PREDATOR_E_1
  percent_chance 25 #define PREDATOR_E_2
end_random

#const PHLAND 647 /* Constable Richemont (Dead) Land-only placeholder. */
#MKCONSTS

#DIRLABELS

start_random
  percent_chance 34 #define NO_PONDS
  percent_chance 33 #define ONE_POND
  percent_chance 33 #define TWO_PONDS
end_random

if NO_PONDS
else
  start_random
    percent_chance 50 #define SAME_MIRROR
    percent_chance 50 #define OPPOSITE_MIRROR
  end_random

  if ONE_POND
    start_random
      percent_chance 34
        #define FISH_1_3
        #define FISH_2_3
        #define FISH_3_3
        #define FISH_4_3
      percent_chance 33
        #define FISH_1_4
        #define FISH_2_4
        #define FISH_3_4
        #define FISH_4_4
      percent_chance 33
        #define FISH_1_5
        #define FISH_2_5
        #define FISH_3_5
        #define FISH_4_5
    end_random
    if SAME_MIRROR
      start_random
        percent_chance 7
          #define POND_1_0
          #define POND_4_0
        percent_chance 7
          #define POND_1_1
          #define POND_4_1
        percent_chance 7
          #define POND_1_2
          #define POND_4_2
        percent_chance 7
          #define POND_1_3
          #define POND_4_3
        percent_chance 7
          #define POND_1_4
          #define POND_4_4
        percent_chance 7
          #define POND_1_5
          #define POND_4_5
        percent_chance 8
          #define POND_1_6
          #define POND_4_6
        percent_chance 7
          #define POND_2_0
          #define POND_3_0
        percent_chance 7
          #define POND_2_1
          #define POND_3_1
        percent_chance 7
          #define POND_2_2
          #define POND_3_2
        percent_chance 7
          #define POND_2_3
          #define POND_3_3
        percent_chance 7
          #define POND_2_4
          #define POND_3_4
        percent_chance 7
          #define POND_2_5
          #define POND_3_5
        percent_chance 8
          #define POND_2_6
          #define POND_3_6
      end_random
    elseif OPPOSITE_MIRROR
      start_random
        percent_chance 7
          #define POND_1_0
          #define POND_3_0
        percent_chance 7
          #define POND_1_1
          #define POND_3_1
        percent_chance 7
          #define POND_1_2
          #define POND_3_2
        percent_chance 7
          #define POND_1_3
          #define POND_3_3
        percent_chance 7
          #define POND_1_4
          #define POND_3_4
        percent_chance 7
          #define POND_1_5
          #define POND_3_5
        percent_chance 8
          #define POND_1_6
          #define POND_3_6
        percent_chance 7
          #define POND_2_0
          #define POND_4_0
        percent_chance 7
          #define POND_2_1
          #define POND_4_1
        percent_chance 7
          #define POND_2_2
          #define POND_4_2
        percent_chance 7
          #define POND_2_3
          #define POND_4_3
        percent_chance 7
          #define POND_2_4
          #define POND_4_4
        percent_chance 7
          #define POND_2_5
          #define POND_4_5
        percent_chance 8
          #define POND_2_6
          #define POND_4_6
      end_random
    endif
  elseif TWO_PONDS
    if SAME_MIRROR
      start_random
        percent_chance 34
          #define FISH_1_3
          #define FISH_4_3
        percent_chance 33
          #define FISH_1_4
          #define FISH_4_4
        percent_chance 33
          #define FISH_1_5
          #define FISH_4_5
      end_random
      start_random
        percent_chance 34
          #define FISH_2_3
          #define FISH_3_3
        percent_chance 33
          #define FISH_2_4
          #define FISH_3_4
        percent_chance 33
          #define FISH_2_5
          #define FISH_3_5
      end_random
      start_random
        percent_chance 14
          #define POND_1_0
          #define POND_4_0
        percent_chance 14
          #define POND_1_1
          #define POND_4_1
        percent_chance 14
          #define POND_1_2
          #define POND_4_2
        percent_chance 14
          #define POND_1_3
          #define POND_4_3
        percent_chance 14
          #define POND_1_4
          #define POND_4_4
        percent_chance 15
          #define POND_1_5
          #define POND_4_5
        percent_chance 15
          #define POND_1_6
          #define POND_4_6
      end_random
      start_random
        percent_chance 14
          #define POND_2_0
          #define POND_3_0
        percent_chance 14
          #define POND_2_1
          #define POND_3_1
        percent_chance 14
          #define POND_2_2
          #define POND_3_2
        percent_chance 14
          #define POND_2_3
          #define POND_3_3
        percent_chance 14
          #define POND_2_4
          #define POND_3_4
        percent_chance 15
          #define POND_2_5
          #define POND_3_5
        percent_chance 15
          #define POND_2_6
          #define POND_3_6
      end_random
    elseif OPPOSITE_MIRROR
      start_random
        percent_chance 50
          #define FISH_1_3
          #define FISH_3_3
        percent_chance 50
          #define FISH_1_4
          #define FISH_3_4
      end_random
      start_random
        percent_chance 50
          #define FISH_2_3
          #define FISH_4_3
        percent_chance 50
          #define FISH_2_4
          #define FISH_4_4
      end_random
      start_random
        percent_chance 14
          #define POND_1_0
          #define POND_3_0
        percent_chance 14
          #define POND_1_1
          #define POND_3_1
        percent_chance 14
          #define POND_1_2
          #define POND_3_2
        percent_chance 14
          #define POND_1_3
          #define POND_3_3
        percent_chance 14
          #define POND_1_4
          #define POND_3_4
        percent_chance 15
          #define POND_1_5
          #define POND_3_5
        percent_chance 15
          #define POND_1_6
          #define POND_3_6
      end_random
      start_random
        percent_chance 14
          #define POND_2_0
          #define POND_4_0
        percent_chance 14
          #define POND_2_1
          #define POND_4_1
        percent_chance 14
          #define POND_2_2
          #define POND_4_2
        percent_chance 14
          #define POND_2_3
          #define POND_4_3
        percent_chance 14
          #define POND_2_4
          #define POND_4_4
        percent_chance 15
          #define POND_2_5
          #define POND_4_5
        percent_chance 15
          #define POND_2_6
          #define POND_4_6
      end_random
    endif
  endif
endif

<PLAYER_SETUP>
direct_placement
ai_info_map_type BLACK_FOREST 0 0 0
behavior_version 2

#SETPHATTR

effect_percent SET_ATTRIBUTE PHLAND ATTR_RADIUS_1 50
effect_percent SET_ATTRIBUTE PHLAND ATTR_RADIUS_2 50
effect_amount SET_ATTRIBUTE PHLAND ATTR_DEAD_ID -1
effect_amount SET_ATTRIBUTE PHLAND ATTR_TERRAIN_ID 8
effect_amount SET_ATTRIBUTE PHLAND ATTR_LINE_OF_SIGHT 0
effect_amount SET_ATTRIBUTE PHLAND ATTR_HITPOINTS 0
effect_amount SET_ATTRIBUTE PHLAND ATTR_STORAGE_VALUE 0

<LAND_GENERATION>
base_terrain DESERT
enable_waves 0

#BFLANDS

<ELEVATION_GENERATION>
create_elevation 3 {
  enable_balanced_elevation
  base_terrain BASE_TERRAIN
  number_of_clumps 17
  number_of_tiles 700
  set_scale_by_size
}

create_elevation 4 {
  enable_balanced_elevation
  base_terrain DESERT
  number_of_clumps 12
  number_of_tiles 1500
  set_scale_by_size
}

<TERRAIN_GENERATION>

#REPEAT(16)
  create_terrain BASE_TERRAIN {
    base_terrain DESERT
    land_percent 100
    number_of_clumps 4096
    terrain_mask 2
  }
#END_REPEAT

#REPEAT(16)
  create_terrain BASE_FOREST {
    base_layer DESERT
    base_terrain BASE_TERRAIN
    land_percent 100
    number_of_clumps 4096
    set_avoid_player_start_areas 20
    spacing_to_other_terrain_types 5
  }
#END_REPEAT

#REPEAT(16)
  create_terrain BASE_TERRAIN {
    base_layer DESERT
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Standard-ish BF forest and terrain generation. */
create_terrain BASE_FOREST {
  base_terrain BASE_TERRAIN
  land_percent 18
  number_of_clumps 24
  set_avoid_player_start_areas 17
  spacing_to_other_terrain_types 4
}

create_terrain BASE_TERRAIN {
  base_terrain BASE_FOREST
  land_percent 7
  number_of_clumps 9
}

/* Forest Mixing */
create_terrain MIXING_FOREST {
  land_percent 10
  number_of_clumps 128
  base_terrain BASE_FOREST
}

create_terrain MIXING_FOREST {
  land_percent 1
  number_of_clumps 256
  base_terrain BASE_FOREST
}

/* Terrain Mixing */
create_terrain LEAVES {
  base_terrain BASE_TERRAIN
  spacing_to_other_terrain_types 1
  land_percent 3
  number_of_clumps 26
  if MASK_LEAVES
    terrain_mask 2
  endif
}

create_terrain LEAVES {
  base_terrain BASE_TERRAIN
  spacing_to_other_terrain_types 1
  land_percent 1
  number_of_clumps 40
  if MASK_LEAVES
    terrain_mask 2
  endif
}

/* Grass Mixing */
create_terrain GRASS_PATCH {
  base_terrain BASE_TERRAIN
  land_percent 1
  number_of_clumps 24
  spacing_to_other_terrain_types 1
  /* set_scale_by_groups */
}

/* Water Mixing */
create_terrain MED_WATER {
  base_terrain WATER
  spacing_to_other_terrain_types 2
  land_percent 1
  number_of_clumps 4
  set_avoid_player_start_areas
  set_flat_terrain_only
  terrain_mask 2
}

<CONNECTION_GENERATION>
accumulate_connections

create_connect_all_players_land {
  replace_terrain BASE_TERRAIN BASE_TERRAIN
  replace_terrain LEAVES LEAVES
  replace_terrain GRASS_PATCH GRASS_PATCH
  replace_terrain BASE_FOREST BASE_TERRAIN
  replace_terrain MIXING_FOREST BASE_TERRAIN
  replace_terrain WATER SHALLOW_TERRAIN
  terrain_cost BASE_TERRAIN 1
  terrain_cost BASE_FOREST 2
  terrain_cost MIXING_FOREST 2
  terrain_cost WATER 12
  terrain_cost MED_WATER 14
  terrain_cost LEAVES 2
  terrain_cost GRASS_PATCH 1
  terrain_size BASE_TERRAIN 0 0
  terrain_size BASE_FOREST 5 1
  terrain_size MIXING_FOREST 5 1
  terrain_size WATER 2 1
  terrain_size MED_WATER 2 1
  terrain_size LEAVES 0 0
  terrain_size GRASS_PATCH 0 0
}

create_connect_teams_lands {
  replace_terrain BASE_TERRAIN ROAD_TERRAIN
  replace_terrain LEAVES ROAD_TERRAIN
  replace_terrain GRASS_PATCH ROAD_TERRAIN
  replace_terrain BASE_FOREST ROAD_TERRAIN
  replace_terrain MIXING_FOREST ROAD_TERRAIN
  replace_terrain WATER SHALLOW_TERRAIN
  replace_terrain MED_WATER SHALLOW_TERRAIN
  terrain_cost BASE_TERRAIN 2
  terrain_cost BASE_FOREST 7
  terrain_cost MIXING_FOREST 7
  terrain_cost WATER 25
  terrain_cost MED_WATER 25
  terrain_cost LEAVES 2
  terrain_cost ROAD_TERRAIN 1
  terrain_cost GRASS_PATCH 2
  terrain_size BASE_TERRAIN 1 0
  terrain_size BASE_FOREST 1 0
  terrain_size MIXING_FOREST 1 0
  terrain_size WATER 2 1
  terrain_size MED_WATER 2 1
  terrain_size ROAD_TERRAIN 0 0
  terrain_size LEAVES 1 0
  terrain_size GRASS_PATCH 1 0
}

<OBJECTS_GENERATION>
#OBJECTS9VILS

/* Terrain Blockers Around Houses */
create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in house2
}

/* Player Resources */
/* Player Berries */
create_object FORAGE {
  number_of_objects 6
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 5
  avoid_actor_area box9
  max_distance_to_players 12
  avoid_forest_zone 3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid7
  actor_area forage5
  actor_area_radius 5
  min_distance_to_map_edge 3
}

/* Near Gold */
create_object GOLD {
  number_of_objects 7
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 4
  avoid_actor_area box11
  max_distance_to_players 16
  min_distance_group_placement 2
  avoid_forest_zone 3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage5
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
  min_distance_to_map_edge 3
}

/* Near Stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 3
  avoid_actor_area box11
  max_distance_to_players 18
  min_distance_group_placement 2
  avoid_forest_zone 3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage5
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
  min_distance_to_map_edge 3
}

/* Medium Gold */
create_object GOLD {
  number_of_objects 4
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 3
  avoid_actor_area box17
  max_distance_to_players 26
  min_distance_group_placement 2
  avoid_forest_zone 4
  avoid_actor_area forage5
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
  min_distance_to_map_edge 3
}

/* Far Stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 3
  avoid_actor_area box19
  max_distance_to_players 26
  min_distance_group_placement 2
  avoid_forest_zone 4
  avoid_actor_area forage5
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
  min_distance_to_map_edge 3
}

/* Far Gold */
create_object GOLD {
  number_of_objects 4
  set_place_for_every_player
  set_tight_grouping
  set_circular_placement
  set_gaia_object_only
  max_distance_to_other_zones 3
  avoid_actor_area box17
  max_distance_to_players 28
  min_distance_group_placement 2
  avoid_forest_zone 3
  avoid_actor_area forage5
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
  min_distance_to_map_edge 3
}

/* Player food resources. */
create_object HUNTABLE {
  number_of_objects 4
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  avoid_actor_area box16
  max_distance_to_players 22
  set_circular_placement
  avoid_actor_area forage5
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  min_distance_group_placement 1
}

create_object LUREABLE {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  avoid_actor_area box15
  max_distance_to_players 22
  set_circular_placement
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  min_distance_group_placement 1
  temp_min_distance_group_placement 7
}

create_object HERDABLE_B {
  number_of_objects 2
  number_of_groups 2
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  avoid_actor_area box13
  max_distance_to_players 22
  set_circular_placement
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  min_distance_group_placement 1
  temp_min_distance_group_placement 7
}

create_object HERDABLE_A {
  number_of_objects 3
  set_loose_grouping
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
  set_place_for_every_player
  avoid_forest_zone 1
}

create_object SCOUT {
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
  set_place_for_every_player
  avoid_forest_zone 1
}

/* Map Gold */
create_object GOLD {
  number_of_objects 3
  number_of_groups 3
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  temp_min_distance_group_placement 20
  avoid_actor_area box40
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  actor_area map_gold
  actor_area_radius 15
}

/* Map Stone */
create_object STONE {
  number_of_objects 4
  number_of_groups 2
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  temp_min_distance_group_placement 20
  avoid_actor_area box40
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  avoid_actor_area map_gold
}

/* Far Boar/Javelina */
/* Uses a placeholder to spawn the hunt to ensure all 3 spawn. */
create_object PHLAND {
  number_of_objects 3
  set_place_for_every_player
  group_placement_radius 4
  set_loose_grouping
  set_gaia_object_only
  set_circular_placement
  avoid_actor_area box30
  max_distance_to_players 40
  min_distance_group_placement 5
  avoid_forest_zone 3
  max_distance_to_other_zones 2
  actor_area far_hunt
  actor_area_radius 20
  avoid_actor_area far_hunt
  second_object LUREABLE
}

/* Captain Fin */
/* Shore fish for each pond. */
create_object SHORE_FISH {
  if FISH_1_3
    number_of_objects 3
  elseif FISH_1_4
    number_of_objects 4
  elseif FISH_1_5
    number_of_objects 5
  endif
  place_on_specific_land_id 21
  set_gaia_object_only
  temp_min_distance_group_placement 1
}

create_object SHORE_FISH {
  if FISH_2_3
    number_of_objects 3
  elseif FISH_2_4
    number_of_objects 4
  elseif FISH_2_5
    number_of_objects 5
  endif
  place_on_specific_land_id 22
  set_gaia_object_only
  temp_min_distance_group_placement 1
}

create_object SHORE_FISH {
  if FISH_3_3
    number_of_objects 3
  elseif FISH_3_4
    number_of_objects 4
  elseif FISH_3_5
    number_of_objects 5
  endif
  place_on_specific_land_id 23
  set_gaia_object_only
  temp_min_distance_group_placement 1
}

create_object SHORE_FISH {
  if FISH_4_3
    number_of_objects 3
  elseif FISH_4_4
    number_of_objects 4
  elseif FISH_4_5
    number_of_objects 5
  endif
  place_on_specific_land_id 24
  set_gaia_object_only
  temp_min_distance_group_placement 1
}

/* Other Map Objects */

/* Relics */
create_object RELIC {
  number_of_objects 5
  set_gaia_object_only
  min_distance_to_players 35
  set_circular_placement
  min_distance_to_map_edge 1
  min_distance_group_placement 2
  avoid_forest_zone 3
  max_distance_to_other_zones 2
  temp_min_distance_group_placement 30
}

/* Player Predators */
create_object PREDATOR {
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement 2
  avoid_actor_area box29
  max_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}

/* Map Predators */
create_object PREDATOR {
  if PREDATOR_A_1
    number_of_objects 1
  elseif PREDATOR_A_2
    number_of_objects 2
  endif
  set_loose_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  min_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}
create_object PREDATOR {
  if PREDATOR_B_1
    number_of_objects 1
  elseif PREDATOR_B_2
    number_of_objects 2
  endif
  set_loose_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  min_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}
create_object PREDATOR {
  if PREDATOR_C_1
    number_of_objects 1
  elseif PREDATOR_C_2
    number_of_objects 2
  endif
  set_loose_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  min_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}
create_object PREDATOR {
  if PREDATOR_D_1
    number_of_objects 1
  elseif PREDATOR_D_2
    number_of_objects 2
  endif
  set_loose_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  min_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}
create_object PREDATOR {
  if PREDATOR_E_1
    number_of_objects 1
  elseif PREDATOR_E_2
    number_of_objects 2
  endif
  set_loose_grouping
  set_gaia_object_only
  min_distance_group_placement 2
  min_distance_to_players 40
  set_circular_placement
  avoid_forest_zone 2
  actor_area predator15
  actor_area_radius 15
  avoid_actor_area predator15
}

/* Map Stragglers */
create_object STRAGGLER {
  number_of_objects 100
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 18
  min_distance_group_placement 2
  temp_min_distance_group_placement 10
}

/* Birds */
create_object BIRD {
  number_of_objects 6
  set_gaia_object_only
  set_scaling_to_map_size
}
