#HEADER_START
/*
 * Hamburger for The Cartographers.
 * Edited by T-West, October 2023.
 *
 * Based on DE's map script.
 */

/* Hamburger 2020 */
#HEADER_END

/* PH SEASONS */

start_random
  percent_chance 12 #define PH_ALPINE
  percent_chance 12 #define PH_DESERT
  percent_chance 12 #define PH_TROPHICALSOUTH
  percent_chance 12 #define PH_TROPHICALEAST
  percent_chance 13 #define PH_AFRICAN
  percent_chance 13 #define PH_ASIAN
  percent_chance 13 #define PH_AUTUMN
  percent_chance 13 #define PH_FROZEN
end_random

if PH_ALPINE
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
elseif PH_DESERT
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1060 /* DLC_GOAT */
elseif PH_TROPHICALSOUTH
  #const HERDABLE_A 833 /* TURKEY */
  #const HERDABLE_B 305 /* DLC_LLAMA */
elseif PH_TROPHICALEAST
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
elseif PH_AFRICAN
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1060 /* DLC_GOAT */
  /* Ensure the African stragglers have 100 Wood, not 150 Acacia. */
  #const STRAGGLER 1051 /* DLC_DRAGONTREE */
elseif PH_ASIAN
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
elseif PH_AUTUMN
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1245 /* DLC_PIG */
elseif PH_FROZEN
  #const HERDABLE_A 594 /* SHEEP */
  #const HERDABLE_B 1060 /* DLC_GOAT */
endif

#include_drs F_seasons.inc

/* Picks an orientation for Relics to spawn. */
start_random
  percent_chance 25 #define RELIC0
  percent_chance 25 #define RELIC1
  percent_chance 25 #define RELIC2
  percent_chance 25 #define RELIC3
end_random

#const PHLAND 647 /* Constable Richemont (Dead) Land-only placeholder. */
#MKCONSTS

<PLAYER_SETUP>
override_map_size 140
random_placement
behavior_version 1
ai_info_map_type COASTAL 0 0 0

effect_amount SET_ATTRIBUTE PHLAND ATTR_DEAD_ID -1
effect_amount SET_ATTRIBUTE PHLAND ATTR_TERRAIN_ID 8
effect_amount SET_ATTRIBUTE PHLAND ATTR_LINE_OF_SIGHT 0
effect_amount SET_ATTRIBUTE PHLAND ATTR_HITPOINTS 0
effect_amount SET_ATTRIBUTE PHLAND ATTR_STORAGE_VALUE 0

#SETPHATTR

#include_drs thebr_setup.inc

<LAND_GENERATION>
base_terrain DLC_ROCK

create_player_lands {
  terrain_type LAYER_A
  land_percent 100
  base_size 10
  border_fuzziness 60
  clumping_factor 20
  set_zone_by_team
  right_border 21
  bottom_border 21
  left_border 21
  top_border 21
  circle_radius 22 0
  other_zone_avoidance_distance 1
}

<TERRAIN_GENERATION>

#include_drs F_ColorCorrection.inc

/* Handle the middle forest. */
#REPEAT(8)
  create_terrain VODA {
    base_terrain DLC_ROCK
    spacing_to_other_terrain_types 2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain ROAD2 {
    base_terrain LAYER_A
    spacing_to_other_terrain_types 2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain DLC_ROCK {
    base_terrain LAYER_A
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain ROAD2 {
    base_terrain DLC_ROCK
    terrain_mask 2
    land_percent 100
    number_of_clumps 4096
    spacing_to_other_terrain_types 6
  }
#END_REPEAT

#REPEAT(8)
  create_terrain WOODIES_B {
    base_layer DLC_ROCK
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAYER_A {
    base_terrain ROAD2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* The extra spacing and other terrain make land around the forest sides. */
#REPEAT(8)
  create_terrain VODA {
    base_terrain DLC_ROCK
    spacing_to_other_terrain_types 5
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAYER_A {
    base_terrain DLC_ROCK
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Create the outside terrain and woodlines. */
create_terrain LAYER_C {
  base_terrain VODA
  number_of_clumps 10
  spacing_to_other_terrain_types 9
  land_percent 60
}

create_terrain WOODIES_C {
  base_terrain LAYER_C
  number_of_clumps 30
  spacing_to_other_terrain_types 6
  land_percent 6
}

/* PH Masking Patch B+C on A */
create_terrain LAYER_B {
  base_terrain LAYER_A
  number_of_clumps 32
  spacing_to_other_terrain_types 0
  land_percent 22
  set_scale_by_size
}

#REPEAT(8)
  create_terrain ROAD {
    base_terrain LAYER_B
    number_of_clumps 1000
    spacing_to_other_terrain_types 2
    land_percent 100
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAYER_A {
    base_terrain LAYER_B
    number_of_clumps 1000
    land_percent 100
    terrain_mask 2
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAYER_A {
    base_terrain LAYER_B
    number_of_clumps 1000
    land_percent 100
    terrain_mask 2
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAYER_B {
    base_terrain ROAD
    number_of_clumps 1000
    land_percent 100
  }
#END_REPEAT

create_terrain LAYER_C {
  base_terrain LAYER_A
  number_of_clumps 32
  spacing_to_other_terrain_types 1
  land_percent 10
  set_scale_by_size
  terrain_mask 1
}

/* PH Masking Patch B+C on A - END */
#define WMASK_VODA
#include_drs F_WaterMasking.inc

if PH_TROPHICALEAST
  create_terrain LAYER_A {
    base_terrain LAYER_C
    number_of_clumps 1000
    land_percent 10
    terrain_mask 1
  }
elseif PH_TROPHICALSOUTH
  create_terrain LAYER_A {
    base_terrain LAYER_C
    number_of_clumps 1000
    land_percent 10
    terrain_mask 1
  }
elseif PH_TROPHICALSOUTH_B
  create_terrain LAYER_A {
    base_terrain LAYER_C
    number_of_clumps 1000
    land_percent 10
    terrain_mask 1
  }
endif

/* LAYER E+F Patch */
create_terrain LAYER_E {
  base_terrain LAYER_A
  number_of_clumps 20
  spacing_to_other_terrain_types 0
  land_percent 2
  set_scale_by_size
  terrain_mask 1
}

create_terrain LAYER_F {
  base_terrain LAYER_A
  number_of_clumps 14
  spacing_to_other_terrain_types 0
  land_percent 1
  set_scale_by_size
  terrain_mask 1
}

create_terrain LAYER_B {
  base_terrain LAYER_C
  number_of_clumps 25
  spacing_to_other_terrain_types 0
  land_percent 20
  set_scale_by_size
}

create_terrain LAYER_E {
  base_terrain LAYER_C
  number_of_clumps 20
  spacing_to_other_terrain_types 0
  land_percent 2
  set_scale_by_size
  terrain_mask 1
}

create_terrain LAYER_F {
  base_terrain LAYER_C
  number_of_clumps 14
  spacing_to_other_terrain_types 0
  land_percent 1
  set_scale_by_size
  terrain_mask 1
}

<OBJECTS_GENERATION>
if RELIC0
  create_actor_area 10 10 relic0 20
  create_actor_area 95 5 relic1 20
  create_actor_area 135 75 relic2 20
  create_actor_area 75 135 relic3 20
  create_actor_area 5 95 relic4 20
elseif RELIC1
  create_actor_area 135 10 relic0 20
  create_actor_area 135 90 relic1 20
  create_actor_area 65 135 relic2 20
  create_actor_area 5 85 relic3 20
  create_actor_area 40 5 relic4 20
elseif RELIC2
  create_actor_area 135 135 relic0 20
  create_actor_area 45 135 relic1 20
  create_actor_area 5 55 relic2 20
  create_actor_area 55 5 relic3 20
  create_actor_area 135 45 relic4 20
elseif RELIC3
  create_actor_area 10 135 relic0 20
  create_actor_area 5 45 relic1 20
  create_actor_area 80 5 relic2 20
  create_actor_area 135 55 relic3 20
  create_actor_area 105 135 relic4 20
endif

create_object RELIC {
  /* number_of_objects 28224 */
  min_distance_group_placement 3
  min_distance_to_players 35
  set_gaia_object_only
  avoid_forest_zone 3
  min_distance_to_map_edge 5
  max_distance_to_other_zones 5
  actor_area_to_place_in relic0
}

create_object RELIC {
  /* number_of_objects 28224 */
  min_distance_group_placement 3
  min_distance_to_players 35
  set_gaia_object_only
  avoid_forest_zone 3
  min_distance_to_map_edge 5
  max_distance_to_other_zones 5
  actor_area_to_place_in relic1
}

create_object RELIC {
  /* number_of_objects 28224 */
  min_distance_group_placement 3
  min_distance_to_players 35
  set_gaia_object_only
  avoid_forest_zone 3
  min_distance_to_map_edge 5
  max_distance_to_other_zones 5
  actor_area_to_place_in relic2
}

create_object RELIC {
  /* number_of_objects 28224 */
  min_distance_group_placement 3
  min_distance_to_players 35
  set_gaia_object_only
  avoid_forest_zone 3
  min_distance_to_map_edge 5
  max_distance_to_other_zones 5
  actor_area_to_place_in relic3
}

create_object RELIC {
  /* number_of_objects 28224 */
  min_distance_group_placement 3
  min_distance_to_players 35
  set_gaia_object_only
  avoid_forest_zone 3
  min_distance_to_map_edge 5
  max_distance_to_other_zones 5
  actor_area_to_place_in relic4
}

/* Highlights the mainland with an actor area for outside objects to avoid. */
create_object PHLAND {
  number_of_objects 28224
  generate_for_first_land_only
  set_gaia_object_only
  set_place_for_every_player
  actor_area mainland
  temp_min_distance_group_placement 4
  actor_area_radius 7
}

/* Isolates the edges of the middle forest. */
create_object PHON {
  number_of_objects 28224
  set_gaia_object_only
  terrain_to_place_on BEACH
  actor_area coast5
  actor_area_radius 5
}

create_object PHON {
  number_of_objects 28224
  set_gaia_object_only
  terrain_to_place_on ICYSHORE
  actor_area coast5
  actor_area_radius 5
}

create_object PHON {
  number_of_objects 28224
  set_gaia_object_only
  terrain_to_place_on WOODIES_B
  actor_area forest5
  actor_area_radius 5
}

create_object PHON {
  number_of_objects 28224
  set_gaia_object_only
  actor_area_to_place_in coast5
  avoid_actor_area forest5
  actor_area coast5-minus-forest5
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 28224
  set_gaia_object_only
  actor_area_to_place_in coast5
  avoid_actor_area coast5-minus-forest5
  actor_area ends3
  actor_area_radius 4
}

#OBJECTS9VILS

/* Near TC Herdables */
create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 7
  max_distance_to_players 9
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in house2
}

/* Player Berries */
create_object FORAGE {
  number_of_objects 6
  set_tight_grouping
  group_placement_radius 2
  set_gaia_object_only
  set_circular_placement
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  avoid_forest_zone 5
  max_distance_to_other_zones 4
  min_distance_group_placement 2
  avoid_actor_area ends3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid6
  actor_area avoid6
  actor_area_radius 6
}

/* Near Gold */
create_object GOLD {
  number_of_objects 7
  set_tight_grouping
  set_gaia_object_only
  set_circular_placement
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 16
  avoid_forest_zone 6
  max_distance_to_other_zones 5
  min_distance_group_placement 2
  avoid_actor_area ends3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid6
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
}

/* Near Stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_circular_placement
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 18
  avoid_forest_zone 6
  max_distance_to_other_zones 5
  min_distance_group_placement 2
  avoid_actor_area ends3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid6
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
}

/* Medium Gold */
create_object GOLD {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_circular_placement
  set_place_for_every_player
  min_distance_to_players 13
  max_distance_to_players 21
  avoid_forest_zone 5
  max_distance_to_other_zones 4
  min_distance_group_placement 2
  avoid_actor_area ends3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid6
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
}

/* Far Stone */
create_object STONE {
  number_of_objects 4
  set_tight_grouping
  set_gaia_object_only
  group_placement_radius 2
  set_circular_placement
  set_place_for_every_player
  min_distance_to_players 13
  max_distance_to_players 21
  avoid_forest_zone 5
  max_distance_to_other_zones 4
  min_distance_group_placement 2
  avoid_actor_area ends3
  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area avoid6
  avoid_actor_area avoid7
  actor_area avoid7
  actor_area_radius 7
}

/* Player Deer */
create_object HUNTABLE {
  number_of_objects 4
  set_place_for_every_player
  set_gaia_object_only
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 16
  max_distance_to_players 17
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  min_distance_group_placement 1
  avoid_actor_area avoid6
  avoid_actor_area ends3
  actor_area hunt7
  actor_area_radius 7
  avoid_actor_area hunt7
}

/* Player Boar */
create_object LURABLE_A {
  number_of_objects 2
  temp_min_distance_group_placement 7
  set_place_for_every_player
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 14
  max_distance_to_players 16
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  min_distance_group_placement 1
  avoid_actor_area ends3
  actor_area lure7
  actor_area_radius 7
  avoid_actor_area lure7
}

/* Player Far Sheep */
create_object HERDABLE_B {
  number_of_objects 2
  set_place_for_every_player
  set_gaia_object_only
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 14
  max_distance_to_players 17
  max_distance_to_other_zones 2
  min_distance_group_placement 1
  avoid_actor_area ends3
  avoid_forest_zone 2
  max_distance_to_other_zones 3
  actor_area herd7
  actor_area_radius 7
  avoid_actor_area herd7
}

create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
  avoid_actor_area herd7
}

/* DISTANT GOLD */
create_object GOLD {
  number_of_groups 180
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 14
  min_distance_group_placement 3
  temp_min_distance_group_placement 35
  if TINY_MAP
    max_distance_to_other_zones 2
  elseif SMALL_MAP
    max_distance_to_other_zones 2
  elseif MEDIUM_MAP
    max_distance_to_other_zones 3
  else
    max_distance_to_other_zones 3
  endif
  avoid_actor_area mainland
  avoid_forest_zone 3
  actor_area 4210
  actor_area_radius 12
  min_distance_to_map_edge 3
}

/* DISTANT STONE */
create_object STONE {
  number_of_groups 150
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 14
  min_distance_group_placement 3
  temp_min_distance_group_placement 40
  if TINY_MAP
    max_distance_to_other_zones 2
  elseif SMALL_MAP
    max_distance_to_other_zones 2
  elseif MEDIUM_MAP
    max_distance_to_other_zones 3
  else
    max_distance_to_other_zones 3
  endif
  avoid_actor_area mainland
  avoid_forest_zone 3
  avoid_actor_area 4210
  min_distance_to_map_edge 3
}

/* Captain Fin */
create_object FISH_A {
  number_of_objects 600
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 4
  temp_min_distance_group_placement 6
}

create_object MELKARYBA {
  number_of_objects 999
  set_scaling_to_map_size
  temp_min_distance_group_placement 8
  set_gaia_object_only
}

/* Captain Fin end */

/* Birds */
create_object BIRDS_A {
  number_of_objects 4
  set_scaling_to_map_size
}

/* Outside map stragglers. */
create_object STRAGGLER {
  number_of_objects 30
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 18
  min_distance_to_map_edge 2
  min_distance_group_placement 2
  max_distance_to_other_zones 4
  avoid_actor_area mainland
  avoid_forest_zone 3
}
