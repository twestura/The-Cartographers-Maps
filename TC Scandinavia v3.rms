#HEADER_START
/*
 * Scandinavia for The Cartographers.
 * Edited by T-West, October 2023.
 *
 * Based on DE's Arabia.
 */
#HEADER_END

#const HERDABLE_A 594 /* SHEEP */
#const HERDABLE_B 1245 /* DLC_PIG */
#const STRAGGLER 413 /* SNOWPINETREE */
#const ICE_NAVIGABLE 26 /* Ice that can be sailed on. */

#MKCONSTS

start_random
  percent_chance 50 #define TB
  percent_chance 50 #define LR
end_random

<PLAYER_SETUP>
random_placement
behavior_version 1
ai_info_map_type SCANDANAVIA 0 0 0

#SETPHATTR

<LAND_GENERATION>
base_terrain SNOW
create_player_lands {
  terrain_type SNOW
  land_percent 30
  base_size 18
  border_fuzziness 5
  circle_radius 28 5
  if TB
    left_border 12
    right_border 12
  elseif LR
    top_border 12
    bottom_border 12
  endif
}

if TB
  create_land {
    terrain_type WATER
    land_percent 100
    base_size 4
    border_fuzziness 7
    left_border 92
    land_position 98 50
  }
  create_land {
    terrain_type WATER
    land_percent 100
    base_size 4
    border_fuzziness 7
    right_border 92
    land_position 2 50
  }
elseif LR
  create_land {
    terrain_type WATER
    land_percent 100
    base_size 4
    border_fuzziness 7
    bottom_border 92
    land_position 50 2
  }
  create_land {
    terrain_type WATER
    land_percent 100
    base_size 4
    border_fuzziness 7
    top_border 92
    land_position 50 98
  }
endif

<ELEVATION_GENERATION>
create_elevation 3 {
  base_terrain SNOW
  enable_balanced_elevation
  number_of_tiles 1000
  number_of_clumps 10
}

create_elevation 4 {
  base_terrain SNOW
  enable_balanced_elevation
  number_of_tiles 2000
  number_of_clumps 20
}

<CLIFF_GENERATION>
cliff_type CT_SNOW
min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff 4
max_length_of_cliff 8
min_distance_cliffs 11
cliff_curliness 50

<TERRAIN_GENERATION>
color_correction CC_WINTER

/* Mixing Water */
create_terrain ICE_NAVIGABLE {
  base_terrain WATER
  land_percent 2
  number_of_clumps 20
  spacing_to_other_terrain_types 3
}

create_terrain MED_WATER {
  base_terrain WATER
  land_percent 7
  number_of_clumps 10
  spacing_to_other_terrain_types 2
}

create_terrain MED_WATER {
  base_terrain WATER
  land_percent 1
  number_of_clumps 32
  terrain_mask 1
  spacing_to_other_terrain_types 1
}

create_terrain DEEP_WATER {
  base_terrain MED_WATER
  land_percent 1
  number_of_clumps 32
  terrain_mask 1
  spacing_to_other_terrain_types 1
}

create_terrain WATER {
  base_terrain MED_WATER
  land_percent 1
  number_of_clumps 32
  terrain_mask 1
  spacing_to_other_terrain_types 1
}

/* Forests */
create_terrain SNOW_FOREST {
  base_terrain SNOW
  land_percent 10
  number_of_clumps 14
  set_scale_by_groups
  set_avoid_player_start_areas
  spacing_to_other_terrain_types 5
}

create_terrain PINE_FOREST {
  base_terrain SNOW_FOREST
  land_percent 1
  number_of_clumps 256
  set_scale_by_groups
}

create_terrain DLC_FORESTDEAD {
  base_terrain SNOW_FOREST
  land_percent 1
  number_of_clumps 256
  set_scale_by_groups
}

/* Frozen Ponds */
create_terrain ICE {
  base_terrain SNOW
  land_percent 1
  number_of_clumps 4
  set_scale_by_groups
  set_avoid_player_start_areas 30
  spacing_to_other_terrain_types 2
}

/* Terrain Patches */
create_terrain GRASS_SNOW {
  base_terrain SNOW
  land_percent 9
  number_of_clumps 20
  spacing_to_other_terrain_types 1
  set_scale_by_groups
}

create_terrain GRASS2 {
  base_terrain GRASS_SNOW
  number_of_clumps 10
  land_percent 2
  spacing_to_other_terrain_types 1
  set_scale_by_groups
}

create_terrain DIRT {
  base_terrain SNOW
  land_percent 2
  number_of_clumps 24
  spacing_to_other_terrain_types 3
  set_scale_by_groups
  terrain_mask 1
}

<OBJECTS_GENERATION>

/* Captain Fin */
create_object PHON {
  number_of_objects 9999
  terrain_to_place_on ICYSHORE
  set_gaia_object_only
  actor_area all_shore0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 9999
  terrain_to_place_on ICYSHORE
  set_gaia_object_only
  actor_area all_shore1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99999
  terrain_to_place_on WATER
  set_gaia_object_only
  actor_area water0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 99999
  terrain_to_place_on MED_WATER
  set_gaia_object_only
  actor_area water0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 99999
  terrain_to_place_on DEEP_WATER
  set_gaia_object_only
  actor_area water0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 99999
  set_gaia_object_only
  terrain_to_place_on ICE_NAVIGABLE
  actor_area ice_navigable0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 99999
  set_gaia_object_only
  actor_area_to_place_in all_shore1
  avoid_actor_area all_shore0
  avoid_actor_area water0
  avoid_actor_area ice_navigable0
  actor_area coast_tiles
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99999
  set_gaia_object_only
  actor_area_to_place_in coast_tiles
  terrain_to_place_on ICYSHORE
  actor_area coast_ice1
  actor_area_radius 1
}

create_object SHORE_FISH {
  number_of_objects 9999
  set_gaia_object_only
  temp_min_distance_group_placement 4
  actor_area_to_place_in coast_ice1
  actor_area coast_fish0
  actor_area_radius 0
  terrain_to_place_on WATER
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in coast_fish0
  actor_area shore_fish3
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in coast_fish0
  actor_area coast_fish4
  actor_area_radius 4
}

create_object SHORE_FISH {
  number_of_objects 9999
  set_gaia_object_only
  actor_area shore_fish3
  actor_area_radius 3
  avoid_actor_area coast_fish4
  temp_min_distance_group_placement 7
  terrain_to_place_on WATER
}

create_object SALMON {
  number_of_objects 9999
  temp_min_distance_group_placement 8
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 4
  avoid_actor_area shore_fish3
  actor_area salmon3
  actor_area_radius 3
}

/* Keeps the Marlin off of the ice. */
create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  terrain_to_place_on ICE_NAVIGABLE
  actor_area marlin_avoider
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  terrain_to_place_on ICYSHORE
  actor_area marlin_avoider
  actor_area_radius 1
}

create_object MARLIN1 {
  number_of_objects 9999
  temp_min_distance_group_placement 24
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 7
  avoid_actor_area shore_fish3
  avoid_actor_area salmon3
  avoid_actor_area marlin_avoider
}

#OBJECTS9VILS
create_object PHON {
  number_of_objects 4
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in box0
  actor_area box40
  actor_area_radius 40
}

/* PLAYER RESOURCES */
/* Near Gold */
create_object GOLD {
  number_of_objects 7
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 12
  max_distance_to_players 16
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 3
  avoid_cliff_zone 3

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7

  actor_area maingold7
  actor_area_radius 7
}

/* Near Stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 18
  set_circular_placement
  avoid_forest_zone 3
  avoid_cliff_zone 3
  max_distance_to_other_zones 7
  min_distance_group_placement 7

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7

  actor_area mainstone7
  actor_area_radius 7
}

/* Medium Gold */
create_object GOLD {
  number_of_objects 4
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 18
  max_distance_to_players 26
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 2
  avoid_cliff_zone 2

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7

  actor_area medgold7
  actor_area_radius 7
}

/* Far Stone */
create_object STONE {
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 20
  max_distance_to_players 26
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 2
  avoid_cliff_zone 2

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7

  actor_area farstone7
  actor_area_radius 7
}

/* Far Gold */
create_object GOLD {
  number_of_objects 4
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  set_circular_placement
  min_distance_to_players 25
  max_distance_to_players 30
  max_distance_to_other_zones 7
  avoid_forest_zone 2
  avoid_cliff_zone 2

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7
  avoid_actor_area fargold7

  actor_area fargold7
  actor_area_radius 7
}

/* Player Food */
create_object DEER {
  number_of_objects 8
  set_place_for_every_player
  set_loose_grouping
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 14
  max_distance_to_players 17
  min_distance_group_placement 1
  avoid_forest_zone 4
  avoid_cliff_zone 2
  max_distance_to_other_zones 2
}

create_object BOAR {
  number_of_objects 3
  set_place_for_every_player
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 16
  max_distance_to_players 17
  min_distance_group_placement 1
  avoid_forest_zone 2
  avoid_cliff_zone 2
  max_distance_to_other_zones 2
}

create_object HERDABLE_B {
  number_of_objects 2
  set_place_for_every_player
  set_gaia_object_only
  set_circular_placement
  set_loose_grouping
  min_distance_to_players 22
  max_distance_to_players 25
  min_distance_group_placement 1
  avoid_forest_zone 2
  avoid_cliff_zone 2
  max_distance_to_other_zones 3
  actor_area herd7
  actor_area_radius 7
  avoid_actor_area herd7
}

create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 7
  max_distance_to_players 9
}

create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
}

/* MAP GOLD */
create_object GOLD {
  number_of_objects 5
  number_of_groups 4
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only

  avoid_forest_zone 2
  avoid_cliff_zone 3
  max_distance_to_other_zones 2
  min_distance_to_map_edge 4
  temp_min_distance_group_placement 25
  avoid_actor_area box40

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7
  avoid_actor_area fargold7

  actor_area mapgold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in mapgold1
  actor_area mapgold4
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in mapgold1
  actor_area mapgold9
  actor_area_radius 8
}

/* MAP STONE */
create_object STONE {
  number_of_objects 3
  number_of_groups 3
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only

  avoid_forest_zone 2
  avoid_cliff_zone 3
  max_distance_to_other_zones 2
  min_distance_to_map_edge 4
  temp_min_distance_group_placement 25
  avoid_actor_area box40

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7
  avoid_actor_area fargold7
  avoid_actor_area mapgold9

  actor_area mapstone1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in mapstone1
  actor_area mapstone4
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in mapstone1
  actor_area mapstone9
  actor_area_radius 8
}

/* RELICS */
create_object RELIC {
  number_of_objects 5
  set_gaia_object_only
  avoid_actor_area box30
  temp_min_distance_group_placement 40
  max_distance_to_other_zones 4
  min_distance_to_map_edge 4
  avoid_forest_zone 2
  avoid_cliff_zone 4

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7
  avoid_actor_area fargold7
  avoid_actor_area mapgold4
  avoid_actor_area mapstone4

  actor_area relic2
  actor_area_radius 2
}

/* MAP STRAGGLERS */
create_object SNOWPINETREE {
  number_of_objects 30
  set_scaling_to_map_size
  set_gaia_object_only
  avoid_actor_area box16
  temp_min_distance_group_placement 12
  max_distance_to_other_zones 4
  min_distance_to_map_edge 2
  avoid_forest_zone 2
  avoid_cliff_zone 4

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area farstone7
  avoid_actor_area fargold7
  avoid_actor_area mapgold4
  avoid_actor_area mapstone4
  avoid_actor_area relic2
}

/* Birds */
create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
