#HEADER_START
/*
 * "The Snake" for The Cartographers.
 * Idea by the Viper, script by T-West, October 2023.
 */
#HEADER_END

#const REED 1350 /* Tree (Reeds) */
#const RAINFOREST_GRASS 83

#const HERDABLE_A 1243 /* DLC_GOOSE */
#const STRAGGLER 1146 /* DLC_RAINTREE */

/* Randomize assignments of lands to players. */
start_random
  percent_chance 50 #define FLIP
end_random

#MKCONSTS

<PLAYER_SETUP>
direct_placement
ai_info_map_type VALLEY 0 0 0
behavior_version 1

#SETPHATTR

<LAND_GENERATION>
base_terrain DLC_MANGROVESHALLOW
enable_waves 0

create_land {
  terrain_type RAINFOREST_GRASS
  land_position 15 rnd(43,47)
  base_size 18
  other_zone_avoidance_distance 4
  zone 1
  if FLIP
    assign_to AT_TEAM 2 0 0
  else
    assign_to AT_TEAM 1 0 0
  endif
}

create_land {
  terrain_type RAINFOREST_GRASS
  land_position rnd(43,47) 15
  base_size 18
  other_zone_avoidance_distance 4
  zone 1
  if FLIP
    assign_to AT_TEAM 1 0 0
  else
    assign_to AT_TEAM 2 0 0
  endif
}

create_land {
  terrain_type RAINFOREST_GRASS
  land_position rnd(53,57) 80
  base_size 18
  other_zone_avoidance_distance 4
  zone 2
  if FLIP
    assign_to AT_TEAM 2 0 0
  else
    assign_to AT_TEAM 1 0 0
  endif
}

create_land {
  terrain_type RAINFOREST_GRASS
  border_fuzziness 100
  land_position 80 rnd(53,57)
  base_size 18
  other_zone_avoidance_distance 4
  zone 2
  if FLIP
    assign_to AT_TEAM 1 0 0
  else
    assign_to AT_TEAM 2 0 0
  endif
}

/* Border lands */
#SNAKEBORDERS

<ELEVATION_GENERATION>
create_elevation 4 {
  enable_balanced_elevation
  base_terrain RAINFOREST_GRASS
  number_of_tiles 2000
  number_of_clumps 20
}

create_elevation 3 {
  enable_balanced_elevation
  base_terrain RAINFOREST_GRASS
  number_of_tiles 500
  number_of_clumps 5
}

<TERRAIN_GENERATION>
#REPEAT(4)
  create_terrain DLC_ROCK {
    base_terrain DLC_MANGROVESHALLOW
    land_percent 100
    number_of_clumps 4096
    spacing_to_other_terrain_types 5
  }
#END_REPEAT

#REPEAT(4)
  create_terrain RAINFOREST_GRASS {
    base_terrain DLC_MANGROVESHALLOW
    land_percent 100
    number_of_clumps 4096
    terrain_mask 2
    spacing_to_other_terrain_types 5
  }
#END_REPEAT

#REPEAT(4)
  create_terrain ROAD {
    base_layer DLC_MANGROVESHALLOW
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(4)
  create_terrain RAINFOREST_GRASS {
    base_terrain DLC_MANGROVESHALLOW
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(4)
  create_terrain DLC_MANGROVESHALLOW {
    base_terrain ROAD
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Forests */
create_terrain DLC_RAINFOREST {
  base_terrain RAINFOREST_GRASS
  spacing_to_other_terrain_types 5
  land_percent 10
  number_of_clumps 24
  set_avoid_player_start_areas
}

/* Land Mixing */
create_terrain DLC_JUNGLELEAVES {
  base_terrain RAINFOREST_GRASS
  land_percent 9
  number_of_clumps 24
  spacing_to_other_terrain_types 1
  terrain_mask 1
}

create_terrain GRASS2 {
  base_terrain RAINFOREST_GRASS
  land_percent 7
  number_of_clumps 32
  spacing_to_other_terrain_types 1
  terrain_mask 1
}

create_terrain DIRT3 {
  base_terrain RAINFOREST_GRASS
  land_percent 35
  number_of_clumps 64
  spacing_to_other_terrain_types 1
  terrain_mask 1
  height_limits 1 5
}

/* Shallow Mixing */
create_terrain DLC_NEWSHALLOW {
  base_terrain DLC_MANGROVESHALLOW
  land_percent 1
  number_of_clumps 32
  terrain_mask 1
}

create_terrain DLC_WATER5 {
  base_terrain DLC_MANGROVESHALLOW
  number_of_tiles 64
  number_of_clumps 32
  terrain_mask 1
}

<CONNECTION_GENERATION>
/* DLC_BEACH2 is Beach, White, Vegetation */
/* Use players lands instead of all lands to avoid crashes. */
create_connect_all_players_land {
  replace_terrain BEACH DLC_BEACH2
  terrain_size BEACH 999 0
}

<OBJECTS_GENERATION>
create_actor_area 20 20 left_corner 15
create_actor_area 147 147 right_corner 15

create_actor_area 83 83 inside2 81
create_actor_area 84 83 inside2 81
create_actor_area 83 84 inside2 81
create_actor_area 84 84 inside2 81

/* Map Corner Golds */
create_object GOLD {
  number_of_objects 3
  number_of_groups 2
  set_tight_grouping
  set_gaia_object_only
  group_placement_radius 2
  min_distance_to_players 45
  set_circular_placement
  
  avoid_forest_zone 3
  
  temp_min_distance_group_placement 9
  min_distance_group_placement 4

  actor_area_to_place_in left_corner
  actor_area corner_gold
  actor_area_radius 9
}

create_object GOLD {
  number_of_objects 3
  number_of_groups 2
  set_tight_grouping
  set_gaia_object_only
  group_placement_radius 2
  min_distance_to_players 45
  set_circular_placement
  
  avoid_forest_zone 3
  
  temp_min_distance_group_placement 9
  min_distance_group_placement 4

  actor_area_to_place_in right_corner
  actor_area corner_gold
  actor_area_radius 9
}

#OBJECTS9VILS

create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
}

/* PLAYER RESOURCES */
/* Forage */
create_object FORAGE {
  number_of_objects 6
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area box8
  max_distance_to_players 12
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 3

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage6
  avoid_actor_area corner_gold

  actor_area forage6
  actor_area_radius 6
}

/* Near Gold */
create_object GOLD {
  number_of_objects 7
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area box10
  max_distance_to_players 16
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 3
  avoid_cliff_zone 3

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage6
  avoid_actor_area maingold7
  avoid_actor_area corner_gold

  actor_area maingold7
  actor_area_radius 7
}

/* Near Stone */
create_object STONE {
  number_of_objects 5
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  avoid_actor_area box12
  max_distance_to_players 18
  set_circular_placement
  avoid_forest_zone 3
  avoid_cliff_zone 3
  max_distance_to_other_zones 7
  min_distance_group_placement 7

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage6
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area corner_gold

  actor_area mainstone7
  actor_area_radius 7
}

/* Medium Gold */
create_object GOLD {
  number_of_objects 4
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  avoid_actor_area box16
  max_distance_to_players 26
  set_circular_placement
  max_distance_to_other_zones 7
  avoid_forest_zone 2
  avoid_cliff_zone 2

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage6
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area corner_gold

  actor_area medgold7
  actor_area_radius 7
}

/* Far Gold */
create_object GOLD {
  number_of_objects 4
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  set_circular_placement
  avoid_actor_area box23
  max_distance_to_players 30
  max_distance_to_other_zones 7
  avoid_forest_zone 2
  avoid_cliff_zone 2

  avoid_actor_area house3
  avoid_actor_area straggler2
  avoid_actor_area forage6
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area fargold7
  avoid_actor_area corner_gold

  actor_area fargold7
  actor_area_radius 7
}

/* Player Food */
create_object DEER {
  number_of_objects 3
  set_place_for_every_player
  set_gaia_object_only
  set_circular_placement
  set_loose_grouping
  avoid_actor_area box20
  max_distance_to_players 26
  avoid_forest_zone 2
  avoid_actor_area forage6
  max_distance_to_other_zones 6
  min_distance_to_map_edge 10
}

create_object BOAR {
  number_of_objects 2
  set_place_for_every_player
  temp_min_distance_group_placement 3
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 14
  max_distance_to_players 17
  avoid_forest_zone 2
  min_distance_to_map_edge 5
}

create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_circular_placement
  set_loose_grouping
  min_distance_to_players 7
  max_distance_to_players 9
}

/* Resources in the middle river. */
create_object PHON {
  number_of_objects 9999
  terrain_to_place_on DLC_BEACH2
  set_gaia_object_only
  actor_area beach1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 9999
  terrain_to_place_on DLC_BEACH2
  set_gaia_object_only
  actor_area beach4
  actor_area_radius 4
}

create_object PHON {
  set_gaia_object_only
  number_of_objects 999
  temp_min_distance_group_placement 3
  terrain_to_place_on DLC_MANGROVESHALLOW
  second_object SHORE_FISH

  actor_area shore_fish0
  actor_area_radius 0
}

/* Relics on the shallow. */
create_object RELIC {
  number_of_objects 5
  set_gaia_object_only
  terrain_to_place_on DLC_MANGROVESHALLOW
  avoid_actor_area beach1
  temp_min_distance_group_placement 25
  min_distance_to_map_edge 2
  min_distance_group_placement 1
}

/* Stone near the shallow. */
create_object STONE {
  number_of_objects 2
  number_of_groups 100
  set_tight_grouping
  temp_min_distance_group_placement 20
  min_distance_to_map_edge 4
  avoid_forest_zone 2
  min_distance_group_placement 3
  set_gaia_object_only
  min_distance_to_players 30
  set_circular_placement
  terrain_to_place_on RAINFOREST_GRASS
  avoid_actor_area beach1
  actor_area_to_place_in beach4
  actor_area stone_middle
  actor_area_radius 1
}

create_object PHON {
  set_gaia_object_only
  number_of_objects 999
  actor_area_to_place_in shore_fish0
  actor_area reed_avoider
  actor_area_radius 2
}

create_object PHON {
  set_gaia_object_only
  number_of_objects 9999
  actor_area_to_place_in stone_middle
  actor_area reed_avoider
  actor_area_radius 2
}

/* Place Reeds in the middle. */
create_object REED {
  set_gaia_object_only
  number_of_objects 3
  number_of_groups 100
  temp_min_distance_group_placement 11
  group_placement_radius 2
  set_tight_grouping
  actor_area_to_place_in beach4
  avoid_actor_area reed_avoider
  terrain_to_place_on RAINFOREST_GRASS
}

create_object REED {
  set_gaia_object_only
  number_of_objects 2
  number_of_groups 100
  temp_min_distance_group_placement 13
  group_placement_radius 2
  set_tight_grouping
  actor_area_to_place_in beach4
  avoid_actor_area reed_avoider
  terrain_to_place_on RAINFOREST_GRASS
}

/* Sheep around the edge of the map. */
create_object SHEEP {
  number_of_objects 100
  set_gaia_object_only
  avoid_actor_area inside2
  temp_min_distance_group_placement 28
  min_distance_group_placement 1
  min_distance_to_players 35
  set_circular_placement
}

/* Scatter single Deer around the map. */
create_object DEER {
  number_of_objects rnd(30,40)
  temp_min_distance_group_placement 12
  min_distance_group_placement 3
  set_gaia_object_only
  avoid_forest_zone 1
  min_distance_to_map_edge 3
  min_distance_to_players 27
  set_circular_placement
  max_distance_to_other_zones 14
  min_distance_to_map_edge 3
}

/* Map stragglers */
create_object STRAGGLER {
  number_of_objects 100
  temp_min_distance_group_placement 15
  min_distance_to_map_edge 1
  set_gaia_object_only
  terrain_to_place_on RAINFOREST_GRASS
  min_distance_group_placement 1
  avoid_forest_zone 2
  max_distance_to_other_zones 5
  min_distance_to_map_edge 4

  avoid_actor_area reed_avoider
  avoid_actor_area beach4
  min_distance_to_players 18
  set_circular_placement

  avoid_actor_area forage6
  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area fargold7
}

/* Scenery */
create_object DLC_ROCKJUNGLE {
  number_of_objects 10
  avoid_forest_zone 3
  min_distance_group_placement 3
  terrain_to_place_on RAINFOREST_GRASS
  temp_min_distance_group_placement 25
  min_distance_group_placement 5
  set_gaia_object_only
  min_distance_to_map_edge 3
  avoid_forest_zone 2

  avoid_actor_area reed_avoider
  avoid_actor_area beach4
  min_distance_to_players 18
  set_circular_placement

  avoid_actor_area maingold7
  avoid_actor_area mainstone7
  avoid_actor_area medgold7
  avoid_actor_area fargold7
}
