/*
 * El Dorado for The Cartographers.
 * Edited by T-West, October 2023.
 *
 * Insipired by the WSVG 2015 version by HJ.
 */

/* TODO terrain restrictions for sea rocks */

#CIRCLE_LABELS(30,135)

#const HERDABLE_A 833 /* TURKEY */
#const HERDABLE_B 305 /* DLC_LLAMA */
#const STRAGGLER 414 /* JUNGLETREE */
#const PHWATER 645 /* Jean de Lorrain (Dead) Water-only placeholder. */

#MKCONSTS

if TINY_MAP
  if 2_PLAYER_GAME
    #define MANUAL_PLACEMENT
  endif
endif

<PLAYER_SETUP>
if MANUAL_PLACEMENT
  direct_placement
else  
  random_placement
endif
behavior_version 1
ai_info_map_type GOLD_RUSH 0 0 0

#SETPHATTR

/* Use the water placeholders, since the rocks don't seem to work. */
effect_amount SET_ATTRIBUTE PHWATER ATTR_DEAD_ID -1
effect_amount SET_ATTRIBUTE PHWATER ATTR_TERRAIN_ID 19
effect_amount SET_ATTRIBUTE PHWATER ATTR_LINE_OF_SIGHT 0
effect_amount SET_ATTRIBUTE PHWATER ATTR_HITPOINTS 0
effect_amount SET_ATTRIBUTE PHWATER ATTR_STORAGE_VALUE 0

/* Allow objects to spawn outside of SHALLOW terrain. */
effect_amount SET_ATTRIBUTE JUNGLETREE ATTR_TERRAIN_ID 20
effect_amount SET_ATTRIBUTE BOAR ATTR_TERRAIN_ID 20
effect_amount SET_ATTRIBUTE DEER ATTR_TERRAIN_ID 20
effect_amount SET_ATTRIBUTE GOLD ATTR_TERRAIN_ID 20
effect_amount SET_ATTRIBUTE STONE ATTR_TERRAIN_ID 20

/* Allows the OLD_STONE_HEAD objects to avoid the beaches/water. */
effect_amount SET_ATTRIBUTE OLD_STONE_HEAD ATTR_TERRAIN_ID 8

<LAND_GENERATION>

base_terrain DEEP_WATER

if MANUAL_PLACEMENT
  create_land {
    terrain_type ROAD
    land_percent 100
    left_border 8
    right_border 9
    top_border 8
    bottom_border 9
    border_fuzziness 100
    assign_to_player 1
    zone 1
    other_zone_avoidance_distance 2
    #CIRCLE_POSITION_P1(30,135)
  }

  create_land {
    terrain_type ROAD
    land_percent 100
    left_border 8
    right_border 9
    top_border 8
    bottom_border 9
    border_fuzziness 100
    assign_to_player 2
    zone 1
    other_zone_avoidance_distance 2
    #CIRCLE_POSITION_P2(30,135)
  }
else
  create_player_lands {
    terrain_type ROAD
    land_percent 100
    left_border 8
    right_border 8
    top_border 8
    bottom_border 8
    border_fuzziness 60
    circle_radius 31 2
    other_zone_avoidance_distance 2
    zone 1
  }
endif

/* MIDDLE */
create_land {
	terrain_type DESERT
	land_percent 100
  left_border 40
  right_border 40
  top_border 40
  bottom_border 40
	clumping_factor 5
	border_fuzziness 45
  other_zone_avoidance_distance 2
  zone 2
}

<ELEVATION_GENERATION>
create_elevation 3 {
  base_terrain ROAD
  if TINY_MAP
    number_of_clumps 6
    number_of_tiles 1000
  elseif SMALL_MAP
    number_of_clumps 9
    number_of_tiles 1500
  elseif MEDIUM_MAP
    number_of_clumps 12
    number_of_tiles 2000
  elseif LARGE_MAP
    number_of_clumps 17
    number_of_tiles 2857
  elseif HUGE_MAP
    number_of_clumps 21
    number_of_tiles 3429
  elseif GIGANTIC_MAP
    number_of_clumps 25
    number_of_tiles 4143
  elseif LUDIKRIS_MAP
    number_of_clumps 99
    number_of_tiles 16429
  endif
  enable_balanced_elevation
}

create_elevation 1 {
  base_terrain ROAD
  if TINY_MAP
    number_of_clumps 3
    number_of_tiles 25
  elseif SMALL_MAP
    number_of_clumps 5
    number_of_tiles 38
  elseif MEDIUM_MAP
    number_of_clumps 6
    number_of_tiles 50
  elseif LARGE_MAP
    number_of_clumps 9
    number_of_tiles 71
  elseif HUGE_MAP
    number_of_clumps 10
    number_of_tiles 86
  elseif GIGANTIC_MAP
    number_of_clumps 12
    number_of_tiles 104
  elseif LUDIKRIS_MAP
    number_of_clumps 49
    number_of_tiles 410
  endif
  enable_balanced_elevation
}

<TERRAIN_GENERATION>

/* Covers the ring of water of the middle land. */
#REPEAT(10)
  create_terrain WATER {
    base_terrain DEEP_WATER
    spacing_to_other_terrain_types 3
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(10)
  create_terrain DLC_JUNGLEGRASS {
    land_percent 100
    base_terrain ROAD
    number_of_clumps 4096
    set_avoid_player_start_areas 22
  }
#END_REPEAT

#REPEAT(10)
  create_terrain SHALLOW {
    land_percent 100
    base_terrain ROAD
    spacing_to_other_terrain_types 6
    number_of_clumps 4096
  }
#END_REPEAT

/* PLAYER FORESTS */
#REPEAT(25)
  create_terrain ROAD2 {
    base_terrain ROAD
    land_percent 100
    number_of_clumps 1
  }

  create_terrain FOREST {
    base_terrain ROAD2
    number_of_tiles 60
    number_of_clumps 1
    spacing_to_other_terrain_types 1
  }

  create_terrain JUNGLE {
    base_terrain ROAD2
    number_of_tiles 60
    number_of_clumps 1
    spacing_to_other_terrain_types 1
  }

  create_terrain SNOW {
    base_terrain ROAD2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(10)
  create_terrain DLC_JUNGLEGRASS {
    base_terrain ROAD
    land_percent 100
    number_of_clumps 4096
  }

  create_terrain DLC_JUNGLEGRASS {
    base_terrain DEEP_WATER
    land_percent 100
    number_of_clumps 4096
  }

  /* Smaller areas for global forests to avoid. */
  create_terrain DLC_JUNGLEGRASS {
    base_terrain SNOW
    land_percent 100
    number_of_clumps 4096
    set_avoid_player_start_areas 20
  }
#END_REPEAT

/* Global Forests */
create_terrain JUNGLE {
  land_percent 9
  number_of_clumps 24
  set_scale_by_groups
  base_terrain DLC_JUNGLEGRASS
  spacing_to_other_terrain_types 4
}

/* Cover up Terrain */
#REPEAT(10)
  create_terrain JUNGLE {
    base_terrain FOREST
    land_percent 100
    number_of_clumps 4096
  }

  create_terrain DLC_JUNGLEGRASS {
    base_terrain SNOW
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Make player shallows smaller, place grass between them and player forests. */
#REPEAT(10)
  create_terrain DLC_JUNGLEGRASS {
    base_terrain SHALLOW
    land_percent 100
    number_of_clumps 4096
    set_avoid_player_start_areas 9
  }
#END_REPEAT

/* Terrain Patches */
create_terrain LEAVES {
  base_terrain DLC_JUNGLEGRASS
  land_percent 20
  number_of_clumps 16
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain GRASS3 {
  base_terrain LEAVES
  land_percent 10
  number_of_clumps 20
  set_scale_by_groups
  spacing_to_other_terrain_types 2
  set_avoid_player_start_areas
  terrain_mask 1
}

create_terrain LEAVES {
  base_terrain DLC_JUNGLEGRASS
  land_percent 3
  number_of_clumps 3
  set_avoid_player_start_areas
  set_scale_by_groups
  terrain_mask 1
}

create_terrain DIRT {
  base_terrain DESERT
  land_percent 1
  number_of_clumps 32
  terrain_mask 1
}

/* MIXING WATER */
create_terrain MED_WATER {
  base_terrain WATER
  terrain_mask 2
  spacing_to_other_terrain_types 2
  land_percent 10
  number_of_clumps 6
  set_scale_by_groups
}

create_terrain MED_WATER {
  base_terrain WATER
  number_of_clumps 6
  set_scale_by_groups
  spacing_to_other_terrain_types 3
  land_percent 40
}

create_terrain DEEP_WATER {
  base_terrain MED_WATER
  number_of_clumps 6
  set_scale_by_groups
  spacing_to_other_terrain_types 5
  land_percent 5
}

create_terrain DEEP_WATER {
  base_terrain MED_WATER
  number_of_clumps 20
  set_scale_by_groups
  spacing_to_other_terrain_types 1
  land_percent 1
  terrain_mask 1
}

create_terrain MED_WATER {
  base_terrain DEEP_WATER
  number_of_clumps 30
  set_scale_by_groups
  spacing_to_other_terrain_types 1
  land_percent 1
}

create_terrain WATER {
  base_terrain MED_WATER
  number_of_clumps 30
  set_scale_by_groups
  spacing_to_other_terrain_types 1
  land_percent 1
  terrain_mask 1
}

<OBJECTS_GENERATION>
create_object PHON {
  set_gaia_object_only
  terrain_to_place_on DESERT
  number_of_objects 9999
  actor_area desert1
  actor_area_radius 1
}

create_object PHON {
  set_gaia_object_only
  terrain_to_place_on DESERT
  number_of_objects 9999
  actor_area desert5
  actor_area_radius 5
}

/* Edge fish. */
create_object SHORE_FISH {
  number_of_objects 40
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 4
  min_distance_group_placement 3
}

create_object SALMON {
  number_of_objects 140
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 15
  actor_area big_fish7
  actor_area_radius 7
  min_distance_group_placement 2
  if TINY_MAP
    max_distance_to_other_zones 2
  else
    max_distance_to_other_zones 4
  endif
}

create_object SNAPPER {
  number_of_objects 140
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 7
  avoid_actor_area big_fish7
  min_distance_group_placement 2
  if TINY_MAP
    max_distance_to_other_zones 2
  else
    max_distance_to_other_zones 4
  endif
}

/* Use a placeholder for `SEA_ROCKS_2`, which seem to behave strangely. */
create_object PHWATER {
  number_of_objects 4
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 3
  min_distance_to_map_edge 2
  min_distance_group_placement 2
  temp_min_distance_group_placement 20
  second_object SEA_ROCKS_2
}

/* MAP REVEALER */
create_object MAP_REVEALER {
	terrain_to_place_on DESERT
	number_of_objects	9999
	set_place_for_every_player
}

/* Player Objects */
create_object TOWN_CENTER {
  set_place_for_every_player
  max_distance_to_players 0
}

#TCCENTER
#TCBOXES
#VISION
#TC9VILS
#STRAGGLER9VILS

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  terrain_to_place_on BEACH
  actor_area_to_place_in box11

  actor_area player_beach0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_beach0
  actor_area player_beach1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_beach0
  actor_area player_beach2
  actor_area_radius 2
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_beach2
  avoid_actor_area player_beach1
  terrain_to_place_on DLC_JUNGLEGRASS
  actor_area house_area
  actor_area_radius 0
}

create_object PHOFF {
  number_of_objects 2
  set_place_for_every_player
  actor_area_to_place_in house_area
  second_object HOUSE
  temp_min_distance_group_placement 5
  avoid_forest_zone 3

  actor_area house_surrounding
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_beach0
  actor_area player_beach5
  actor_area_radius 5
}

create_object GOLD {
  number_of_objects 2
  number_of_groups 2
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_forest_zone 2
  avoid_actor_area house_surrounding
  avoid_actor_area player_beach1
  actor_area_to_place_in player_beach5
  temp_min_distance_group_placement 7
  terrain_to_place_on DLC_JUNGLEGRASS

  actor_area player_gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_gold1
  actor_area player_gold8
  actor_area_radius 8
}

create_object STONE {
  number_of_objects 1
  number_of_groups 2
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_forest_zone 2
  avoid_actor_area house_surrounding
  avoid_actor_area player_gold8
  avoid_actor_area player_beach1
  actor_area_to_place_in player_beach5
  terrain_to_place_on DLC_JUNGLEGRASS
  temp_min_distance_group_placement 8

  actor_area player_stone1
  actor_area_radius 1
}

create_object MARLIN1 {
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in blocking_seventh_villager
}

create_object PHON {
  number_of_objects 4
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 2
  max_distance_to_players 2
  temp_min_distance_group_placement 1
  find_closest
  actor_area fish_blocker_1
  actor_area_radius 1
}

create_object MARLIN1 {
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 2
  max_distance_to_players 2
  find_closest
  avoid_actor_area fish_blocker_1
}

create_object PHON {
  number_of_objects 2
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 1
  max_distance_to_players 1
  find_closest
  actor_area fish_blocker_2
  actor_area_radius 2
}

create_object MARLIN1 {
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 2
  max_distance_to_players 2
  avoid_actor_area fish_blocker_2
}

/* Player Turkey */
create_object HERDABLE_A {
  number_of_objects 3
  set_loose_grouping
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
}

/* Gaia DEER */
create_object DEER {
  number_of_objects 4
  set_loose_grouping
  set_place_for_every_player
  set_gaia_object_only
  group_placement_radius 3
  min_distance_group_placement 14
  max_distance_to_players 20
  set_circular_placement
  avoid_forest_zone 3
  avoid_actor_area house_surrounding
  terrain_to_place_on DLC_JUNGLEGRASS
}

/* Gaia Boar */
create_object JAVELINA {
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 21
  set_circular_placement
  avoid_forest_zone 2
}

/* Gaia LLAMA */
create_object HERDABLE_B {
  number_of_objects 2
  set_loose_grouping
  set_place_for_every_player
  set_gaia_object_only
  min_distance_to_players 16
  max_distance_to_players 20
  set_circular_placement
  avoid_forest_zone 2
}

create_object SCOUT {
  set_place_for_every_player
  avoid_actor_area box6
  actor_area_to_place_in box9
  avoid_forest_zone 1
}

/* RESOURCES IN THE MIDDLE DESERT */
create_object STONE {
  terrain_to_place_on DESERT
  number_of_objects 4
  number_of_groups 10
  group_variance 1
  set_tight_grouping
  set_gaia_object_only
  set_scaling_to_map_size
  temp_min_distance_group_placement 3
  actor_area mid_stone1
  actor_area_radius 1
}

create_object TERRAIN_BLOCKER {
  set_gaia_object_only
  number_of_objects 9999
  actor_area_to_place_in mid_stone1
}

create_object GOLD {
  terrain_to_place_on DESERT
  number_of_objects 4
  number_of_groups 10
  group_variance 1
  set_tight_grouping
  set_gaia_object_only
  set_scaling_to_map_size
  temp_min_distance_group_placement 3
  actor_area mid_gold1
  actor_area_radius 1
}

/* RELICS */
create_object RELIC {
  if 7_PLAYER_GAME
    number_of_objects 6
  elseif 8_PLAYER_GAME 
    number_of_objects 6
  else
    number_of_objects 5
  endif
  avoid_forest_zone 2
  avoid_actor_area desert5
  min_distance_to_players 22
  set_circular_placement
  if TINY_MAP
    temp_min_distance_group_placement 25
  elseif SMALL_MAP
    temp_min_distance_group_placement 35
  else
    temp_min_distance_group_placement 42
  endif
  if TINY_MAP
    max_distance_to_other_zones 5
  elseif SMALL_MAP
    max_distance_to_other_zones 7
  else
    max_distance_to_other_zones 10
  endif
  actor_area relic5
  actor_area_radius 5
}

/* STRAGGLER TREES */
create_object JUNGLE_TREE {
  number_of_objects 50
  set_gaia_object_only
  set_scaling_to_map_size
  max_distance_to_other_zones 2 /* Keeps trees away from the coast. */
  avoid_actor_area relic5
  avoid_actor_area box18
  avoid_actor_area desert1
  actor_area map_straggler0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in map_straggler0
  actor_area map_straggler1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in map_straggler0
  actor_area map_straggler3
  actor_area_radius 3
}

/* BIRDS */
create_object MACAW {
  number_of_objects 6
  set_gaia_object_only
  set_scaling_to_map_size
}

/* Various scenery. */
create_object CRACKS {
  terrain_to_place_on DESERT
  number_of_objects 2
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 8
  avoid_actor_area mid_stone1
  avoid_actor_area mid_gold1
}

create_object OLD_STONE_HEAD {
  number_of_objects 3
  set_scaling_to_map_size
  set_gaia_object_only
  avoid_forest_zone 2
  temp_min_distance_group_placement 50
  terrain_to_place_on DLC_JUNGLEGRASS
  max_distance_to_other_zones 10
  avoid_actor_area map_straggler3
  avoid_actor_area box31
  avoid_actor_area desert1
  avoid_actor_area relic5
}
