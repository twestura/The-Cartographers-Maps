#HEADER_START
/*
 * Socotra for The Cartographers.
 * Edited by T-West, October 2023.
 */
#HEADER_END

#MKCONSTS

#const HERDABLE_A 1060 /* DLC_GOAT */
#const STRAGGLER 1051 /* DLC_DRAGONTREE */

start_random
percent_chance 50 #const HUNTABLE 1026 /* DLC_OSTRICH */
  percent_chance 50 #const HUNTABLE 1019 /* DLC_ZEBRA */
end_random

start_random
  percent_chance 50 #define SAVCA
  percent_chance 50 #define POUST
end_random

if SAVCA
  #const LAND_TERRAIN 41 /* DLC_SAVANNAH */
  #const PATCH1 6 /* DIRT */
elseif POUST
  #const LAND_TERRAIN 42 /* DLC_DIRT4 */
  #const PATCH1 9 /* GRASS3 */
endif

<PLAYER_SETUP>
random_placement
ai_info_map_type SOCOTRA 0 0 0
behavior_version 1

#SETPHATTR

<LAND_GENERATION>
base_terrain DEEP_WATER
create_player_lands {
  terrain_type LEAVES
  land_percent 100
  base_size 6
  top_border 20
  right_border 20
  bottom_border 20
  left_border 20
  border_fuzziness 20
  circle_radius 17 0
}

<ELEVATION_GENERATION>
create_elevation 5 {
  enable_balanced_elevation
  base_terrain LEAVES
  number_of_tiles 1250
  number_of_clumps 12
}

<TERRAIN_GENERATION>
#REPEAT(8)
  create_terrain LAND_TERRAIN {
    base_terrain LEAVES
    spacing_to_other_terrain_types 12
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

create_terrain DLC_DRAGONFOREST {
  base_terrain LEAVES
  set_avoid_player_start_areas
  land_percent 8
  number_of_clumps 256
}

create_terrain DESERT {
  base_terrain LAND_TERRAIN
  number_of_tiles 120
  number_of_clumps 7
  spacing_to_other_terrain_types 9
  set_avoid_player_start_areas
}

#REPEAT(8)
  create_terrain DLC_DRAGONFOREST {
    base_terrain DESERT
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain LAND_TERRAIN {
    base_terrain LEAVES
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Terrain Patches */
create_terrain PATCH1 {
  base_terrain LAND_TERRAIN
  land_percent 2
  number_of_clumps 15
  set_scale_by_groups
  spacing_to_other_terrain_types 4
  terrain_mask 1
}

create_terrain DLC_DRYGRASS {
  base_terrain LAND_TERRAIN
  land_percent 3
  number_of_clumps 10
  set_scale_by_groups
  terrain_mask 1
}

<OBJECTS_GENERATION>

create_object ROCK {
  number_of_objects 9999
  set_gaia_object_only
  terrain_to_place_on BEACH
  actor_area beach0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in beach0
  actor_area beach3
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in beach0
  actor_area beach6
  actor_area_radius 6
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  actor_area_to_place_in beach0
  actor_area beach15
  actor_area_radius 15
}

create_object ROCK {
  number_of_objects 9999
  set_gaia_object_only
  temp_min_distance_group_placement 12
  terrain_to_place_on DEEP_WATER
  avoid_actor_area beach3
}

/* Create actor areas on the forests near the outside rocks. */
create_object PHON {
  number_of_objects 99999
  set_gaia_object_only
  actor_area_to_place_in beach15
  terrain_to_place_on DLC_DRAGONFOREST
  actor_area outside_forest1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99999
  set_gaia_object_only
  actor_area_to_place_in beach15
  terrain_to_place_on DLC_DRAGONFOREST
  actor_area outside_forest4
  actor_area_radius 4
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99999
  set_gaia_object_only
  actor_area_to_place_in outside_forest1
}

create_object TOWN_CENTER {
  set_place_for_every_player
  max_distance_to_players 0
}

create_object TEMPRARY_REVEALER {
  set_place_for_every_player
  max_distance_to_players 0
}

#TCCENTER
#TCBOXES
#VISION
#TC9VILS
#HOUSEGAP3
#STRAGGLER9VILSSOCOTRA

create_object GOLD {
  number_of_objects 4
  number_of_groups 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  temp_min_distance_group_placement 9
  avoid_forest_zone 3
  set_circular_placement
  min_distance_to_players 11
  max_distance_to_players 14
  avoid_actor_area beach6
  avoid_actor_area house2
  actor_area gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in gold1
  actor_area gold5
  actor_area_radius 4
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in gold1
  actor_area gold7
  actor_area_radius 6
}

create_object STONE {
  number_of_objects 2
  number_of_groups 2
  set_place_for_every_player
  set_gaia_object_only
  set_tight_grouping
  set_circular_placement
  temp_min_distance_group_placement 7
  avoid_forest_zone 2
  min_distance_to_players 11
  max_distance_to_players 15
  avoid_actor_area beach6
  avoid_actor_area house2
  avoid_actor_area gold7
  actor_area stone1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in stone1
  actor_area stone5
  actor_area_radius 4
}

create_object DLC_ORANGEBUSH {
  number_of_objects 6
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  set_circular_placement
  min_distance_to_players 10
  max_distance_to_players 13
  avoid_forest_zone 3
  avoid_actor_area outside_forest4
  avoid_actor_area house2
  avoid_actor_area beach6
  avoid_actor_area gold5
  avoid_actor_area stone5

  actor_area berries1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries5
  actor_area_radius 4
}

/* Players also have a 3-tile group of Gold and a 2-tile group of Gold. */
create_object GOLD {
  number_of_objects 3
  group_placement_radius 2
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_forest_zone 1
  avoid_actor_area outside_forest4
  set_circular_placement
  min_distance_to_players 16
  max_distance_to_players 23

  avoid_actor_area berries5
  avoid_actor_area gold5
  avoid_actor_area stone5
  avoid_actor_area house2
  avoid_actor_area beach6
  avoid_actor_area far_gold5
  actor_area far_gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in far_gold1
  actor_area far_gold5
  actor_area_radius 4
}

create_object GOLD {
  number_of_objects 2
  group_placement_radius 2
  set_tight_grouping
  set_place_for_every_player
  set_gaia_object_only
  avoid_forest_zone 1
  avoid_actor_area outside_forest4
  set_circular_placement
  min_distance_to_players 16
  max_distance_to_players 23

  avoid_actor_area berries5
  avoid_actor_area gold5
  avoid_actor_area stone5
  avoid_actor_area house2
  avoid_actor_area beach6
  avoid_actor_area far_gold5
  actor_area far_gold1
  actor_area_radius 1
}

create_object DLC_ELEPHANT {
  set_gaia_object_only
  set_place_for_every_player
  set_circular_placement
  min_distance_to_players 12
  max_distance_to_players 16
  avoid_forest_zone 1
  find_closest_to_map_center
  actor_area elephant0
  actor_area_radius 0
}

create_object PHON {
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in elephant0
  actor_area elephant5
  actor_area_radius 5
}

create_object HUNTABLE {
  set_place_for_every_player
  number_of_objects 5
  set_loose_grouping
  group_placement_radius 4
  set_gaia_object_only

  min_distance_to_players 10
  max_distance_to_players 16
  set_circular_placement
  avoid_forest_zone 1
  avoid_actor_area beach6
  avoid_actor_area outside_forest4
  avoid_actor_area berries5
  avoid_actor_area elephant5
  actor_area hunt5
  actor_area_radius 5
  avoid_actor_area hunt5
}

create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 7
  max_distance_to_players 9
  avoid_forest_zone 1
  avoid_actor_area hunt5
}

create_object SCOUT {
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
  set_circular_placement
  avoid_forest_zone 1
  find_closest_to_map_edge
}

/* Relics */
create_object RELIC {
  number_of_objects 5
  set_gaia_object_only
  temp_min_distance_group_placement 18
  avoid_actor_area outside_forest4
  min_distance_to_players 16
}

/* Birds */
create_object DLC_STORK {
  number_of_objects 6
  set_scaling_to_map_size
}
