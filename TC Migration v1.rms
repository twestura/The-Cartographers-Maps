/*
 * Migration for The Cartographers.
 * Edited by T-West, October 2023.
 */

#MKCONSTS

#const HERDABLE_A 594 /* SHEEP */
#const PHWATER 645 /* Jean de Lorrain (Dead) Water-only placeholder. */

if TINY_MAP
  if 2_PLAYER_GAME
    #define MANUAL_PLACEMENT
  endif
elseif MEDIUM_MAP
  #define MEDIUM_PLACEMENT
  start_random
    percent_chance 25 #define T1_TOP
    percent_chance 25 #define T1_RIGHT
    percent_chance 25 #define T1_BOTTOM
    percent_chance 25 #define T1_LEFT
  end_random
  start_random
    percent_chance 50 #define OFFSET_CLOCKWISE
    percent_chance 50 #define OFFSET_COUNTERCLOCKWISE
  end_random
endif

#const PLAYER_UNDER 9 /* GRASS3 */
#const MAIN_UNDER 3 /* DIRT3 */

start_random
  percent_chance 20 #define DESERT_MAP
  percent_chance 20 #define ALPINE_MAP
end_random

if DESERT_MAP
  #const SURFACE 6 /* DIRT */
  #const WOODIES 13 /* PALM_DESERT */
  #const WOODIESB 13 /* PALM_DESERT */
  #const PATCH1 14 /* DESERT */
  #const PATCH2 3 /* DIRT3 */
  #const PATCH3 9 /* GRASS3 */
  #const STRAGGLER 351 /* PALMTREE */
  #const FISH_A 455 /* DORADO */
  #const FISH_B 458 /* SNAPPER */
elseif ALPINE_MAP
  #const SURFACE 12 /* GRASS2 */
  #const WOODIES 19 /* PINE_FOREST */
  #const WOODIESB 10 /* FOREST */
  #const PATCH1 9 /* GRASS3 */
  #const PATCH2 3 /* DIRT3 */
  #const PATCH3 0 /* GRASS */
  #const STRAGGLER 350 /* PINETREE */
  #const FISH_A 456 /* SALMON */
  #const FISH_B 458 /* SNAPPER */
else
  #const SURFACE 0 /* GRASS */
  #const WOODIES 10 /* FOREST */
  #const WOODIESB 10 /* FOREST */
  #const PATCH1 6 /* DIRT */
  #const PATCH2 9 /* GRASS3 */
  #const PATCH3 3 /* DIRT3 */
  #const STRAGGLER 349 /* OAKTREE */
  #const FISH_A 455 /* DORADO */
  #const FISH_B 457 /* TUNA */
endif

#MIGRA_LABELS(31,130)

<PLAYER_SETUP>
if MANUAL_PLACEMENT
  direct_placement
elseif MEDIUM_PLACEMENT
  direct_placement
else  
  random_placement
endif
ai_info_map_type MIGRATION 0 0 0
behavior_version 2

#SETPHATTR

/* Use the water placeholders, since the rocks don't seem to work. */
effect_amount SET_ATTRIBUTE PHWATER ATTR_DEAD_ID -1
effect_amount SET_ATTRIBUTE PHWATER ATTR_TERRAIN_ID 19
effect_amount SET_ATTRIBUTE PHWATER ATTR_LINE_OF_SIGHT 0
effect_amount SET_ATTRIBUTE PHWATER ATTR_HITPOINTS 0
effect_amount SET_ATTRIBUTE PHWATER ATTR_STORAGE_VALUE 0

<LAND_GENERATION>
base_terrain WATER

if MANUAL_PLACEMENT
  create_land {
    terrain_type PLAYER_UNDER
    base_size 15
    land_percent 9
    other_zone_avoidance_distance 10
    clumping_factor 30
    assign_to_player 1
    land_id 1
    zone 1
    #MIGRA_POSITION_P1(31,130)
  }

  create_land {
    terrain_type PLAYER_UNDER
    base_size 15
    land_percent 9
    other_zone_avoidance_distance 10
    clumping_factor 30
    assign_to_player 2
    land_id 2
    zone 2
    #MIGRA_POSITION_P2(31,130)
  }

  /* ***FREE BIG ISLAND*** */
  create_land {
    terrain_type MAIN_UNDER                     
    land_percent 100
    border_fuzziness 10
    other_zone_avoidance_distance 10
    land_id 21
    start_random
      percent_chance 25 left_border 10
      percent_chance 25 left_border 11
      percent_chance 25 left_border 12
      percent_chance 25 left_border 13
    end_random
    start_random
      percent_chance 25 right_border 10
      percent_chance 25 right_border 11
      percent_chance 25 right_border 12
      percent_chance 25 right_border 13
    end_random
    start_random
      percent_chance 25 top_border 10
      percent_chance 25 top_border 11
      percent_chance 25 top_border 12
      percent_chance 25 top_border 13
    end_random
    start_random
      percent_chance 25 bottom_border 10
      percent_chance 25 bottom_border 11
      percent_chance 25 bottom_border 12
      percent_chance 25 bottom_border 13
    end_random
  }
elseif MEDIUM_PLACEMENT
  create_land {
    terrain_type PLAYER_UNDER
    clumping_factor 30
    land_percent 6
    base_size 16
    other_zone_avoidance_distance 10
    assign_to AT_TEAM 1 0 0
    if OFFSET_CLOCKWISE
      land_position rnd(20,35) 10
    elseif OFFSET_COUNTERCLOCKWISE
      land_position 10 rnd(20,35)
    endif
    zone 1
  }

  create_land {
    terrain_type PLAYER_UNDER
    clumping_factor 30
    land_percent 6
    base_size 16
    other_zone_avoidance_distance 10
    assign_to AT_TEAM 1 0 0
    if OFFSET_CLOCKWISE
      land_position 90 rnd(20,35)
    elseif OFFSET_COUNTERCLOCKWISE
      land_position rnd(65,80) 10
    endif
    zone 2
  }

  create_land {
    terrain_type PLAYER_UNDER
    clumping_factor 30
    land_percent 6
    base_size 16
    other_zone_avoidance_distance 10
    assign_to AT_TEAM 2 0 0
    if OFFSET_CLOCKWISE
      land_position 10 rnd(65,80)
    elseif OFFSET_COUNTERCLOCKWISE
      land_position rnd(20,35) 90
    endif
    zone 3
  }

  create_land {
    terrain_type PLAYER_UNDER
    clumping_factor 30
    land_percent 6
    base_size 16
    other_zone_avoidance_distance 10
    assign_to AT_TEAM 2 0 0
    if OFFSET_CLOCKWISE
      land_position rnd(65,80) 90
    elseif OFFSET_COUNTERCLOCKWISE
      land_position 90 rnd(65,80)
    endif
    zone 4
  }

  /* ***FREE BIG ISLAND*** */
  create_land {
    zone 9
    terrain_type MAIN_UNDER                     
    land_percent 100
    border_fuzziness 10
    other_zone_avoidance_distance 10
    land_id 21
    if TINY_MAP
      start_random
        percent_chance 25 left_border 10
        percent_chance 25 left_border 11
        percent_chance 25 left_border 12
        percent_chance 25 left_border 13
      end_random
      start_random
        percent_chance 25 right_border 10
        percent_chance 25 right_border 11
        percent_chance 25 right_border 12
        percent_chance 25 right_border 13
      end_random
      start_random
        percent_chance 25 top_border 10
        percent_chance 25 top_border 11
        percent_chance 25 top_border 12
        percent_chance 25 top_border 13
      end_random
      start_random
        percent_chance 25 bottom_border 10
        percent_chance 25 bottom_border 11
        percent_chance 25 bottom_border 12
        percent_chance 25 bottom_border 13
      end_random
    else
      start_random
        percent_chance 25 left_border 13
        percent_chance 25 left_border 14
        percent_chance 25 left_border 15
        percent_chance 25 left_border 16
      end_random
      start_random
        percent_chance 25 right_border 13
        percent_chance 25 right_border 14
        percent_chance 25 right_border 15
        percent_chance 25 right_border 16
      end_random
      start_random
        percent_chance 25 top_border 13
        percent_chance 25 top_border 14
        percent_chance 25 top_border 15
        percent_chance 25 top_border 16
      end_random
      start_random
        percent_chance 25 bottom_border 13
        percent_chance 25 bottom_border 14
        percent_chance 25 bottom_border 15
        percent_chance 25 bottom_border 16
      end_random
    endif
  }
else
  create_player_lands {
    terrain_type PLAYER_UNDER
    circle_radius 60 0
    clumping_factor 30
    if TINY_MAP
      land_percent 18
      base_size 15
      other_zone_avoidance_distance 10
    else
      land_percent 22
      base_size 16
      other_zone_avoidance_distance 10
    endif
  }

  /* ***FREE BIG ISLAND*** */
  create_land {
    terrain_type MAIN_UNDER                     
    land_percent 100
    border_fuzziness 10
    other_zone_avoidance_distance 10
    land_id 21
    if TINY_MAP
      start_random
        percent_chance 25 left_border 10
        percent_chance 25 left_border 11
        percent_chance 25 left_border 12
        percent_chance 25 left_border 13
      end_random
      start_random
        percent_chance 25 right_border 10
        percent_chance 25 right_border 11
        percent_chance 25 right_border 12
        percent_chance 25 right_border 13
      end_random
      start_random
        percent_chance 25 top_border 10
        percent_chance 25 top_border 11
        percent_chance 25 top_border 12
        percent_chance 25 top_border 13
      end_random
      start_random
        percent_chance 25 bottom_border 10
        percent_chance 25 bottom_border 11
        percent_chance 25 bottom_border 12
        percent_chance 25 bottom_border 13
      end_random
    else
      start_random
        percent_chance 25 left_border 13
        percent_chance 25 left_border 14
        percent_chance 25 left_border 15
        percent_chance 25 left_border 16
      end_random
      start_random
        percent_chance 25 right_border 13
        percent_chance 25 right_border 14
        percent_chance 25 right_border 15
        percent_chance 25 right_border 16
      end_random
      start_random
        percent_chance 25 top_border 13
        percent_chance 25 top_border 14
        percent_chance 25 top_border 15
        percent_chance 25 top_border 16
      end_random
      start_random
        percent_chance 25 bottom_border 13
        percent_chance 25 bottom_border 14
        percent_chance 25 bottom_border 15
        percent_chance 25 bottom_border 16
      end_random
    endif
  }
endif

<ELEVATION_GENERATION>
create_elevation 2 {
  base_terrain MAIN_UNDER
  enable_balanced_elevation
  if TINY_MAP
    number_of_clumps 10
    number_of_tiles 800
  elseif SMALL_MAP
    number_of_clumps 12
    number_of_tiles 1969
  elseif MEDIUM_MAP
    number_of_clumps 20
    number_of_tiles 2150
  elseif LARGE_MAP
    number_of_clumps 23
    number_of_tiles 3500
  elseif HUGE_MAP
    number_of_clumps 28
    number_of_tiles 4500
  elseif GIGANTIC_MAP
    number_of_clumps 34
    number_of_tiles 5438
  elseif LUDIKRIS_MAP
    number_of_clumps 134
    number_of_tiles 21563
  endif
}

<TERRAIN_GENERATION>

/*
 * Surround lands again with water to make them more circular and still keep
 * them separated from each other and from the mainland.
 */
#REPEAT(20)
  create_terrain WATER {
    base_terrain PLAYER_UNDER
    land_percent 100
    set_avoid_player_start_areas 30
    number_of_clumps 256
  }
#END_REPEAT

/* Player Forests. */
#REPEAT(25)
  create_terrain DESERT {
    land_percent 100
    base_terrain PLAYER_UNDER
  }

  create_terrain ROAD {
    land_percent 100
    base_terrain DESERT
  }

  #REPEAT(2)
    create_terrain DLC_ROCK {
      base_terrain ROAD
      number_of_tiles 50
      spacing_to_other_terrain_types 4
      set_avoid_player_start_areas 11
      clumping_factor 40
    }

    create_terrain WOODIES {
      base_terrain DLC_ROCK
      number_of_tiles 100
    }
  #END_REPEAT

  #REPEAT(2)
    create_terrain DLC_ROCK {
      base_terrain ROAD
      number_of_tiles 35
      spacing_to_other_terrain_types 3
      set_avoid_player_start_areas 10
      clumping_factor 40
    }

    create_terrain WOODIES {
      base_terrain DLC_ROCK
      number_of_tiles 100
    }
  #END_REPEAT

  create_terrain SNOW {
    base_terrain ROAD
    land_percent 100
  }
#END_REPEAT

/* Cover the player lands with their actual terrain. */
#REPEAT(20)
  create_terrain SURFACE {
    base_terrain PLAYER_UNDER
    land_percent 100
    number_of_clumps 256
  }

  create_terrain SURFACE {
    base_terrain SNOW
    land_percent 100
    number_of_clumps 256
  }

  create_terrain SURFACE {
    base_terrain ROAD
    land_percent 100
    number_of_clumps 256
  }
#END_REPEAT

/* Make the islands smaller. */
#REPEAT(20)
  create_terrain WATER {
    base_terrain SURFACE
    land_percent 100
    set_avoid_player_start_areas 24
    spacing_to_other_terrain_types 2
    number_of_clumps 1000
  }
#END_REPEAT

/* Forests on the mainland. */
create_terrain WOODIES {
  base_terrain MAIN_UNDER
  spacing_to_other_terrain_types 2
  land_percent 4
  number_of_clumps 8
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain WOODIESB {
  base_terrain MAIN_UNDER
  spacing_to_other_terrain_types 3
  land_percent 1
  number_of_clumps 3
  set_avoid_player_start_areas
  set_scale_by_groups
}

/* Cover the mainland. */
#REPEAT(20)
  create_terrain SURFACE {
    base_terrain MAIN_UNDER
    land_percent 100
    number_of_clumps 256
  }
#END_REPEAT

/* Terrain patches. */
/* PRIMARY PATCH */
create_terrain PATCH1 {
  base_terrain SURFACE
  number_of_clumps 8
  spacing_to_other_terrain_types 0
  if DESERT_MAP
    land_percent 8
    terrain_mask 1
  elseif ALPINE_MAP
    land_percent 6
  else
    land_percent 9
    terrain_mask 1
  endif
  set_scale_by_size
}

/* SECONDARY PATCH */
create_terrain PATCH2 {
  base_terrain SURFACE
  number_of_clumps 24
  spacing_to_other_terrain_types 0
  land_percent 2
  set_scale_by_size
  if ALPINE_MAP
    terrain_mask 1
  endif
}

/* TERTIARY PATCH */
create_terrain PATCH3 {
  base_terrain SURFACE
  number_of_clumps 30
  spacing_to_other_terrain_types 0
  land_percent 2
  set_scale_by_size
  if DESERT_MAP
    terrain_mask 1
  elseif ALPINE_MAP
  else
    terrain_mask 1
  endif
}

/* Mixing Water */
create_terrain MED_WATER {
  base_terrain WATER
  number_of_clumps 10
  spacing_to_other_terrain_types 2
  land_percent 60
}

create_terrain DEEP_WATER {
  base_terrain MED_WATER
  number_of_clumps 12
  spacing_to_other_terrain_types 5
  land_percent 8
}

create_terrain MED_WATER {
  base_terrain DEEP_WATER
  number_of_clumps 24
  land_percent 1
  terrain_mask 1
}

create_terrain DEEP_WATER {
  base_terrain MED_WATER
  number_of_clumps 24
  land_percent 1
  terrain_mask 1
}

create_terrain WATER {
  base_terrain MED_WATER
  number_of_clumps 32
  land_percent 2
  terrain_mask 1
  spacing_to_other_terrain_types 2
}

<OBJECTS_GENERATION>
/* Captain Fin */
create_object SHORE_FISH {
  number_of_objects 9999
  set_gaia_object_only
  temp_min_distance_group_placement 6
  min_distance_to_map_edge 4
  actor_area shore_fish3
  actor_area_radius 3
}

create_object FISH_A {
  number_of_objects 170
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 4
  temp_min_distance_group_placement 14
  min_distance_group_placement 2
  min_distance_to_map_edge 2
  avoid_actor_area shore_fish3
  actor_area big_fish8
  actor_area_radius 8
}

create_object FISH_B {
  number_of_objects 170
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 4
  temp_min_distance_group_placement 8
  min_distance_group_placement 2
  min_distance_to_map_edge 2
  avoid_actor_area shore_fish3
  avoid_actor_area big_fish8
}

create_object PHWATER {
  number_of_objects 25
  set_gaia_object_only
  second_object SEA_ROCKS_2
  min_distance_to_map_edge 2
  max_distance_to_other_zones 5
  min_distance_group_placement 2
  avoid_actor_area shore_fish3
  temp_min_distance_group_placement 30
}

#OBJECTS9VILS

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  set_place_for_every_player
  terrain_to_place_on WOODIES
  actor_area_to_place_in box30
  actor_area player_forest1
  actor_area_radius 1
}

create_object TERRAIN_BLOCKER {
  number_of_objects 9999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in player_forest1
}

/* Player Gold */
create_object GOLD {
  set_gaia_object_only
  set_place_for_every_player
  number_of_objects 4
  number_of_groups 2
  set_tight_grouping
  avoid_forest_zone 3
  temp_min_distance_group_placement 8
  max_distance_to_other_zones 4
  min_distance_to_map_edge 2
  avoid_actor_area box7
  actor_area_to_place_in box10
  avoid_actor_area house2

  actor_area player_gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in player_gold1
  actor_area player_gold4
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in player_gold1
  actor_area player_gold7
  actor_area_radius 6
}

/* Player Stone */
create_object STONE {
  set_gaia_object_only
  set_place_for_every_player
  number_of_objects 4
  set_tight_grouping
  avoid_forest_zone 3
  max_distance_to_other_zones 4
  min_distance_to_map_edge 2
  avoid_actor_area box5
  actor_area_to_place_in box12
  avoid_actor_area player_gold7
  avoid_actor_area house2

  actor_area player_stone1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 9999
  set_gaia_object_only
  set_place_for_every_player
  actor_area_to_place_in player_stone1
  actor_area player_stone4
  actor_area_radius 3
}

/* Player Berries */
create_object FORAGE {
  set_gaia_object_only
  set_place_for_every_player
  number_of_objects 6
  set_tight_grouping
  avoid_forest_zone 3
  max_distance_to_other_zones 4
  min_distance_to_map_edge 2
  avoid_actor_area box5
  actor_area_to_place_in box13
  avoid_actor_area player_gold4
  avoid_actor_area player_stone4
  avoid_actor_area house2

  actor_area player_berries1
  actor_area_radius 1
}

/* Player Deer */
create_object DEER {
  number_of_objects 4
  set_place_for_every_player
  set_gaia_object_only
  group_placement_radius 3
  set_loose_grouping
  avoid_forest_zone 2
  max_distance_to_other_zones 2
  set_circular_placement
  min_distance_to_players 12
  max_distance_to_players 14

  avoid_actor_area house1
  avoid_actor_area player_gold1
  avoid_actor_area player_stone1
  avoid_actor_area player_berries1
}

/* Player Sheep */
create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 7
  max_distance_to_players 9
}

create_object SCOUT {
  set_place_for_every_player
  avoid_actor_area box6
  actor_area_to_place_in box9
  avoid_forest_zone 1
}

/* Middle Resources + Relics */
create_object RELIC {
  if 7_PLAYER_GAME
    number_of_objects 6
  elseif 8_PLAYER_GAME
    number_of_objects 6
  else
    number_of_objects 5
  endif
  if TINY_MAP
    temp_min_distance_group_placement 18
    max_distance_to_other_zones 6
  elseif SMALL_MAP
    temp_min_distance_group_placement 22
    max_distance_to_other_zones 6
  elseif MEDIUM_MAP
    temp_min_distance_group_placement 28
    max_distance_to_other_zones 7
  else
    temp_min_distance_group_placement 40
    min_distance_group_placement 3
    max_distance_to_other_zones 8
  endif
  set_gaia_object_only
  avoid_forest_zone 2
  place_on_specific_land_id 21

  actor_area relic0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in relic0
  actor_area relic1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 999
  set_gaia_object_only
  actor_area_to_place_in relic0
  actor_area relic3
  actor_area_radius 3
}

create_object GOLD {
  number_of_objects 3
  group_placement_radius 3
  number_of_groups 9999
  set_tight_grouping
  set_gaia_object_only
  temp_min_distance_group_placement 17
  min_distance_group_placement 4
  max_distance_to_other_zones 4
  avoid_forest_zone 2
  avoid_actor_area relic3
  place_on_specific_land_id 21
}

create_object STONE {
  number_of_objects 3
  number_of_groups 9999
  set_tight_grouping
  set_gaia_object_only
  temp_min_distance_group_placement 26
  min_distance_group_placement 4
  max_distance_to_other_zones 4
  avoid_forest_zone 2
  avoid_actor_area relic3
  place_on_specific_land_id 21
}

create_object FORAGE {
  number_of_objects 5
  number_of_groups 9999
  set_tight_grouping
  set_gaia_object_only
  temp_min_distance_group_placement 42
  max_distance_to_other_zones 6
  min_distance_group_placement 4
  avoid_forest_zone 2
  avoid_actor_area relic3
  place_on_specific_land_id 21
}

create_object STRAGGLER {
  number_of_objects 30
  set_gaia_object_only
  set_scaling_to_map_size
  place_on_specific_land_id 21
  min_distance_group_placement 2
  avoid_actor_area relic3
  max_distance_to_other_zones 2
}

/* Birds */
create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
