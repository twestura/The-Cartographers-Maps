/*
 * Arena for The Cartographers.
 * Edited by T-West, October 2023.
 */

/* Chooses the biome, "else/undefined" is a "grass" type map. */
start_random
  percent_chance 20 #define ASIAN_MAP
  percent_chance 20 #define ALPINE_MAP
end_random

if ASIAN_MAP
  #const OUTSIDE_FOREST 19 /* PINE_FOREST */
  #const MIDDLE_TERRAIN 12 /* GRASS2 */
  #const PLAYER_TERRAIN 0 /* GRASS */
  #const MIDDLE_FOREST 18 /* BAMBOO */
  #const MIDDLE_PATCH_A 9 /* GRASS3 */
  #const MIDDLE_PATCH_B 3 /* DIRT3 */
  #const MIDDLE_PATCH_C 0 /* GRASS */
  #const STRAGGLER 348 /* BAMBOO_TREE */
elseif ALPINE_MAP
  #const OUTSIDE_FOREST 19 /* PINE_FOREST */
  #const MIDDLE_TERRAIN 12 /* GRASS2 */
  #const PLAYER_TERRAIN 0 /* GRASS */
  #const MIDDLE_FOREST 19 /* PINE_FOREST */
  #const MIDDLE_PATCH_A 9 /* GRASS3 */
  #const MIDDLE_PATCH_B 3 /* DIRT3 */
  #const MIDDLE_PATCH_C 0 /* GRASS */
  #const STRAGGLER 350 /* PINETREE */
else
  #const OUTSIDE_FOREST 10 /* FOREST */
  #const MIDDLE_TERRAIN 0 /* GRASS */
  #const PLAYER_TERRAIN 12 /* GRASS3 */
  #const MIDDLE_FOREST 10 /* FOREST */
  #const MIDDLE_PATCH_A 6 /* DIRT */
  #const MIDDLE_PATCH_B 9 /* GRASS3 */
  #const MIDDLE_PATCH_C 3 /* DIRT3 */
  #const STRAGGLER 349 /* OAKTREE */
endif

/* Constants for changing wall HP. */
#const WALL_HP_TECH 71
#const GATE_1 64
#const GATE_2 78
#const GATE_3 81
#const GATE_4 487
#const GATE_5 88
#const GATE_6 91
#const GATE_7 95
#const GATE_8 490
#const GATE_9 659
#const GATE_10 661
#const GATE_11 663
#const GATE_12 665
#const GATE_13 667
#const GATE_14 669
#const GATE_15 671
#const GATE_16 673

#MKCONSTS

#const HERDABLE_A 594 /* SHEEP */
#const HERDABLE_B 1245 /* DLC_PIG */

/* #define DIRECTION0 */
start_random
  percent_chance 4 #define DIRECTION0
  percent_chance 4 #define DIRECTION1
  percent_chance 4 #define DIRECTION2
  percent_chance 4 #define DIRECTION3
  percent_chance 4 #define DIRECTION4
  percent_chance 4 #define DIRECTION5
  percent_chance 4 #define DIRECTION6
  percent_chance 4 #define DIRECTION7
  percent_chance 4 #define DIRECTION8
  percent_chance 4 #define DIRECTION9
  percent_chance 4 #define DIRECTION10
  percent_chance 4 #define DIRECTION11
  percent_chance 4 #define DIRECTION12
  percent_chance 4 #define DIRECTION13
  percent_chance 4 #define DIRECTION14
  percent_chance 4 #define DIRECTION15
  percent_chance 4 #define DIRECTION16
  percent_chance 4 #define DIRECTION17
  percent_chance 4 #define DIRECTION18
  percent_chance 4 #define DIRECTION19
  percent_chance 4 #define DIRECTION20
  percent_chance 4 #define DIRECTION21
  percent_chance 4 #define DIRECTION22
  percent_chance 4 #define DIRECTION23
  percent_chance 4 #define DIRECTION24
end_random

<PLAYER_SETUP>
direct_placement
ai_info_map_type ARENA 0 0 0
behavior_version 1

#SETPHATTR
/* Use 20 LoS on Arena to encompass more of the base. */
effect_amount SET_ATTRIBUTE TEMPORARY_REVEALER ATTR_LINE_OF_SIGHT 20

/* Walls start with 1800 HP */
effect_amount SET_ATTRIBUTE WALL ATTR_HITPOINTS 1800
effect_amount SET_ATTRIBUTE GATE_1 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_2 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_3 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_4 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_5 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_6 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_7 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_8 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_9 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_10 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_11 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_12 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_13 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_14 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_15 ATTR_HITPOINTS 2750
effect_amount SET_ATTRIBUTE GATE_16 ATTR_HITPOINTS 2750
effect_amount DISABLE_TECH WALL_HP_TECH ATTR_DISABLE 71

<LAND_GENERATION>

base_terrain DESERT

#ARENA_CIRCLE_GAPS

create_land {
  terrain_type MIDDLE_TERRAIN
  land_position 50 50
  top_border 17
  right_border 17
  bottom_border 17
  left_border 17
  border_fuzziness 100
  land_id 10
}

#ARENA_PLAYERS_GAPS

<TERRAIN_GENERATION>
/* Cover the placeholder. */
#REPEAT(8)
  create_terrain OUTSIDE_FOREST {
    set_avoid_player_start_areas
    base_terrain DESERT
    spacing_to_other_terrain_types 2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain MIDDLE_TERRAIN {
    base_terrain DESERT
    spacing_to_other_terrain_types 2
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

#REPEAT(8)
  create_terrain PLAYER_TERRAIN {
    base_terrain DESERT
    land_percent 100
    number_of_clumps 4096
  }
#END_REPEAT

/* Small patches of "primary forest" in the middle. */
create_terrain MIDDLE_FOREST {
  base_terrain MIDDLE_TERRAIN
  land_percent 1
  number_of_clumps 3
  set_scale_by_groups
  spacing_to_other_terrain_types 8
}

/* Blending terrain. */
create_terrain MIDDLE_PATCH_A {
  base_terrain MIDDLE_TERRAIN
  land_percent 8
  number_of_clumps 16
  set_scale_by_groups
  terrain_mask 1
}

create_terrain MIDDLE_PATCH_B {
  base_terrain MIDDLE_TERRAIN
  land_percent 4
  number_of_clumps 16
  set_scale_by_groups
  terrain_mask 1
}

create_terrain MIDDLE_PATCH_C {
  base_terrain MIDDLE_TERRAIN
  land_percent 2
  number_of_clumps 4
  set_scale_by_groups
  terrain_mask 1
}

/* Blends on player terrain. */
create_terrain MIDDLE_TERRAIN {
  base_terrain PLAYER_TERRAIN
  land_percent 8
  number_of_clumps 8
  set_scale_by_groups
  terrain_mask 1
}

create_terrain MIDDLE_PATCH_A {
  base_terrain PLAYER_TERRAIN
  land_percent 1
  number_of_clumps 4
  set_scale_by_groups
  terrain_mask 1
}

<OBJECTS_GENERATION>
#OBJECTS9VILS

create_object PHON {
  number_of_objects 4
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in box0
  actor_area box40
  actor_area_radius 40
}

/* Scout */
create_object SCOUT {
  set_place_for_every_player
  avoid_actor_area box6
  actor_area_to_place_in box8
  avoid_actor_area villager1
  avoid_actor_area house1
  avoid_forest_zone 1
  actor_area scout0
  actor_area_radius 1
}

create_object PHON {
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in scout0
  actor_area scout1
  actor_area_radius 1
}

create_object PHON {
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in scout0
  actor_area scout2
  actor_area_radius 2
}

/* WALLS */
create_object WALL {
  set_place_for_every_player
  min_distance_to_players 18
  max_distance_to_players 18

  actor_area wall0
  actor_area_radius 0
}

create_object PHON {
  set_place_for_every_player
  set_gaia_object_only
  max_distance_to_players 0
  actor_area player_center17
  actor_area_radius 17
}

create_object PHON {
  set_place_for_every_player
  set_gaia_object_only
  max_distance_to_players 0
  actor_area player_center_relic
  actor_area_radius 31
}

create_object PHON {
  number_of_objects 672
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall0
  actor_area wall1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 672
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall0
  actor_area wall2
  actor_area_radius 2
}

create_object PHON {
  number_of_objects 672
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall0
  actor_area wall3
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 672
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall0
  actor_area wall4
  actor_area_radius 4
}

/* Placeholders on the forests near the walls. */
create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area player_center17
  max_distance_to_players 19
  actor_area_to_place_in wall3
  terrain_to_place_on OUTSIDE_FOREST
  actor_area wall_forests
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area player_center17
  max_distance_to_players 19
  actor_area_to_place_in wall3
  terrain_to_place_on MIDDLE_FOREST
  actor wall_forests
  actor_area_radius 0
}

/* Places actor areas on Gate centers. */
create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area wall2
  actor_area_to_place_in wall3
  actor_area wall2_blocker
  actor_area_radius 2
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area wall0
  avoid_actor_area wall1
  avoid_actor_area wall2_blocker
  avoid_actor_area wall_forests
  actor_area_to_place_in wall2
  actor_area gate0
  actor_area_radius 0
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gate0
  actor_area gate4
  actor_area_radius 4
}

/* Places terrain blockers near Gates. */
create_object TERRAIN_BLOCKER {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gate4
}

/* Blockers to keep resources away from walls. */
create_object TERRAIN_BLOCKER {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall2
}

create_object PHON {
  number_of_objects 999
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall3
  max_distance_to_players 18
  second_object TERRAIN_BLOCKER
}

/* Berries */
create_object FORAGE {
  number_of_objects 6
  set_place_for_every_player
  set_tight_grouping
  set_gaia_object_only
  avoid_actor_area box8
  actor_area_to_place_in box10
  max_distance_to_players 12
  avoid_actor_area wall_blocker1
  avoid_actor_area house4
  avoid_forest_zone 3
  min_distance_to_map_edge 4
  
  actor_area berries1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries2
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries3
  actor_area_radius 2
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries4
  actor_area_radius 3
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries5
  actor_area_radius 4
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries6
  actor_area_radius 5
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
  actor_area berries7
  actor_area_radius 6
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in berries1
}

/* Near Gold */
create_object GOLD {
  number_of_objects 7
  set_place_for_every_player
  set_gaia_object_only
  set_tight_grouping
  avoid_actor_area box11
  actor_area_to_place_in box15
  avoid_actor_area wall4
  avoid_actor_area wall_blocker1
  avoid_actor_area berries3
  avoid_actor_area house2
  avoid_forest_zone 4
  min_distance_to_map_edge 2
  terrain_to_place_on PLAYER_TERRAIN
  
  actor_area gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gold1
  actor_area gold2
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gold1
  actor_area gold5
  actor_area_radius 4
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gold1
  actor_area gold7
  actor_area_radius 6
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gold1
  actor_area gold9
  actor_area_radius 8
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in gold1
}

/* Near Stone */
create_object STONE {
  number_of_objects 5
  set_place_for_every_player
  set_gaia_object_only
  group_placement_radius 2
  set_tight_grouping
  avoid_actor_area box9
  actor_area_to_place_in box19
  terrain_to_place_on PLAYER_TERRAIN

  avoid_actor_area wall4
  avoid_actor_area wall_blocker1
  avoid_actor_area berries4
  avoid_actor_area gold7
  avoid_forest_zone 2
  min_distance_to_map_edge 2
  
  actor_area stone1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in stone1
  actor_area stone2
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in stone1
  actor_area stone5
  actor_area_radius 4
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in stone1
  actor_area stone6
  actor_area_radius 5
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in stone1
}

/* Medium Gold */
create_object GOLD {
  number_of_objects 4
  set_place_for_every_player
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  avoid_forest_zone 2
  min_distance_to_map_edge 2

  avoid_actor_area box11
  actor_area_to_place_in box18
  terrain_to_place_on PLAYER_TERRAIN

  avoid_actor_area wall3
  avoid_actor_area wall_blocker1

  avoid_actor_area berries3
  avoid_actor_area gold9
  avoid_actor_area stone6

  actor_area med_gold1
  actor_area_radius 1
}

create_object PHON {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area med_gold5
  actor_area_radius 4
}

create_object TERRAIN_BLOCKER {
  number_of_objects 99
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in med_gold1
}

create_object HERDABLE_A {
  number_of_objects 3
  set_place_for_every_player
  set_loose_grouping
  set_circular_placement
  min_distance_to_players 7
  max_distance_to_players 9
}

create_object DEER {
  number_of_objects 4
  set_place_for_every_player
  set_gaia_object_only
  set_loose_grouping
  avoid_actor_area box11
  max_distance_to_players 18
  set_circular_placement
  terrain_to_place_on PLAYER_TERRAIN
  avoid_forest_zone 2
  avoid_actor_area wall2
}

create_object BOAR {
  number_of_objects 2
  set_place_for_every_player
  set_gaia_object_only
  avoid_actor_area box11
  max_distance_to_players 20
  set_circular_placement
  terrain_to_place_on PLAYER_TERRAIN
  avoid_forest_zone 1
  avoid_actor_area wall3
}

create_object HERDABLE_B {
  number_of_objects 2
  set_place_for_every_player
  set_gaia_object_only
  set_loose_grouping
  avoid_actor_area box11
  max_distance_to_players 20
  set_circular_placement
  terrain_to_place_on PLAYER_TERRAIN
  avoid_forest_zone 1
  avoid_actor_area wall2
}

/* Relics */
create_object RELIC {
  place_on_specific_land_id 10
  set_circular_placement
  max_distance_to_players 10
}

create_object PHON {
  place_on_specific_land_id 10
  max_distance_to_players 0
  actor_area center_square
  actor_area_radius 22
}

create_object PHON {
  place_on_specific_land_id 10
  set_circular_placement
  max_distance_to_players 32
  avoid_actor_area player_center_relic
  number_of_objects 1000
  avoid_actor_area center_square
  actor_area middle_relic_avoider
  actor_area_radius 0
}

create_object RELIC {
  number_of_objects 4
  temp_min_distance_group_placement 35
  min_distance_group_placement 1
  place_on_specific_land_id 10
  set_circular_placement
  max_distance_to_players 45
  avoid_actor_area player_center_relic
  avoid_actor_area center_square
  avoid_actor_area middle_relic_avoider
  avoid_forest_zone 1
}

/* Gold and Stone Outside of Walls */
create_object TERRAIN_BLOCKER {
  number_of_objects 1000
  set_place_for_every_player
  set_gaia_object_only
  actor_area_to_place_in wall4
}

create_object GOLD {
  set_place_for_every_player
  avoid_forest_zone 4
  set_gaia_object_only
  set_tight_grouping
  number_of_objects 4
  group_placement_radius 2
  set_gaia_object_only
  min_distance_group_placement 8
  terrain_to_place_on MIDDLE_TERRAIN
  avoid_actor_area box18
  avoid_actor_area wall4
  actor_area_to_place_in box25
}

create_object STONE {
  set_place_for_every_player
  avoid_forest_zone 4
  set_gaia_object_only
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  min_distance_group_placement 8
  terrain_to_place_on MIDDLE_TERRAIN

  avoid_actor_area box18
  avoid_actor_area wall4
  actor_area_to_place_in box25
}

/* Map Gold */
create_object GOLD {
  number_of_objects 3
  number_of_groups 3
  group_variance 1
  group_placement_radius 2
  avoid_forest_zone 4
  set_tight_grouping
  set_gaia_object_only
  temp_min_distance_group_placement 12
  terrain_to_place_on MIDDLE_TERRAIN
  min_distance_to_players 40
  min_distance_group_placement 4
  actor_area mapgold9
  actor_area_radius 9
}

/* Map Stone */
create_object STONE {
  number_of_objects 4
  number_of_groups 2
  group_variance 1
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 40
  avoid_actor_area mapgold9
  temp_min_distance_group_placement 12
  avoid_forest_zone 4
  min_distance_group_placement 4
}

/* Map Wolves */
create_object WOLF {
  number_of_objects 6
  set_gaia_object_only
  terrain_to_place_on MIDDLE_TERRAIN
  min_distance_to_players 30
  temp_min_distance_group_placement 20
  avoid_forest_zone 2
  avoid_actor_area mapgold4
  avoid_actor_area mapstone4
  min_distance_group_placement 4
}

/* Map stragglers */
create_object STRAGGLER {
  number_of_objects 30
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement 12
  avoid_forest_zone 3
  min_distance_to_players 10
  avoid_actor_area wall2
  avoid_actor_area gold5
  avoid_actor_area stone5
  avoid_actor_area berries5
  avoid_actor_area med_gold5
  avoid_actor_area mapgold4
}

/* Birds */
create_object HAWK {
  number_of_objects 6
  set_scaling_to_map_size
}
